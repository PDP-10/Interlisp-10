(FILECREATED "24-SEP-74 16:59:07" BRKDWN.;2 11534  

     changes to:  BRKDWNCOMS

     previous date: "13-SEP-74 02:17:53" BRKDWN.;1)


  (LISPXPRINT (QUOTE BRKDWNCOMS)
	      T T)
  [RPAQQ BRKDWNCOMS ((FNS * BRKDWNFNS)
	  (VARS BRKDWNTYPES BRKDWNFN (BRKDWNTYPE (QUOTE TIME))
		(BRKDWNLST)
		BRKDWNFLTFMT)
	  [P (SETQ BDLST (VAG (IPLUS (LOC (ARRAY 1 1))
				     2]
	  (BLOCKS * BRKDWNBLOCKS)
	  (DECLARE: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS
		    (ADDVARS (NLAMA BREAKDOWN)
			     (NLAML BRKDWNCONSES BRKDWNTIME]
  (RPAQQ BRKDWNFNS (BREAKDOWN BRKDWN1 BRKDWNTIME BRKDWNCONSES RESULTS RESULTS1))
(DEFINEQ

(BREAKDOWN
  [NLAMBDA FNS

          (* NOTE: This file contains largely machine DEPENDENT code.
          namely heavy useage of VAG/LOC to avoid boxing and unboxing 
          numbers. This usage is for efficiency sake only, since this 
          file attempts to do some fairly accurate measurements.)


    (PROG (N)
          (CONSCOUNT 0)
          (CLOSER (LOC (CAR (QUOTE BDLST)))
		  0)

          (* BDLST is initialized to point to the first cell of an 
          unboxed array and is used for storing the last value of the 
          statistic to be measured.)


          (SETQ BDPTR NIL)
          (COND
	    (BRKDWNTYPE 

          (* BRKDWN1 initializes BRKDWNTERP and BRKDWNLABEL and 
          compiles a measuring function, when necessary, for the 
          measurement indicated by BRKDWNTYPE.
          BRKDWNTYPE is initially set to TIME.)


			(BRKDWN1)))
          [SETQ BRKDWNLST
	    (MAPCONC BRKDWNLST (FUNCTION (LAMBDA (X)
			 (COND
			   ((AND (EQ (CAADDR (GETD (CAR X)))
				     (QUOTE BRKDWN2))
				 (NOT (MEMB (CAR X)
					    FNS)))

          (* This enables both adding to and subtracting from the 
          BREAKDOWN list. If functions originally on BRKDWNLST are 
          still broken, they are kept. Then the new functions are 
          added.)


			     (LIST X]
          [COND
	    (FNS
	      (SETQ N 1)
	      (SETQ BRKDWNLST
		(NCONC
		  BRKDWNLST
		  (MAPCONC
		    FNS
		    (FUNCTION (LAMBDA (X)
			(COND
			  ((NUMBERP X)
			    (SETQ N X)
			    NIL)
			  (T
			    (MAPCONC
			      (BREAK0 X T NIL (QUOTE BRKDWN2))
			      (FUNCTION (LAMBDA (X)
				  (COND
				    ((LISTP X)
				      (PRINT X T T)
				      NIL)
				    (T
				      (PROG (A TEM)
					    (SETQ A (ARRAY 2 2))
					    (RPLACD
					      (CDADDR (GETD X))
					      (SETQ TEM
						(LIST
						  (VAG (IPLUS (LOC A)
							      2))
						  (VAG (IPLUS (LOC A)
							      3))
						  N)))

          (* Form of brokendown function is 
          (BRKDWN2 FORM X1 X2 N) where X1 is a pointer to a cell 
          (in the unboxed portion of an array) that contains is the 
          (negative of the) parameter being measured and and X2 is a 
          pointer to another cell containing the number of times the 
          function has been called. N is number of times FORM is to be 
          evaluated. If N is greater than 1, FORM should not involve 
          any side effects since it will be performed more than once.)


					    (RETURN (LIST (CONS X TEM]

          (* I.e. The form for BRKDWNLST is a list of elements of the 
          form (fn x1 x2 N))


          [MAPC BRKDWNLST (FUNCTION (LAMBDA (FNS)
		    (CLOSER (LOC (CADR FNS))
			    0)
		    (CLOSER (LOC (CADDR FNS))
			    0]

          (* If a completely new BREAKDOWN was done, this isn't really 
          necessary, but it may have been just an additive BREAKDOWN, 
          so counters for old functions should be zeroed.
          Note that BREAKDOWN of NIL just zeroes counters without 
          unbreaking any functions. Note also that BRKDWNTYPE can be 
          changed without unbreaking and rebreaking, since redefining 
          the function BRKDWN2 will take care of everything.)


          (RETURN (MAPCAR BRKDWNLST (FUNCTION CAR])

(BRKDWN1
  [LAMBDA NIL
    (PROG (X Y)
          (SETQ LAPFLG (SETQ SVFLG (SETQ LCFIL)))
          (SETQ STRF (SETQ LSTFIL T))
          [COND
            ((NULL (SETQ X (ASSOC BRKDWNTYPE BRKDWNTYPES)))

          (* * Form of each entry on BRKDWNTYPES is 
          (NAME FORM1 FORM2) e.g. (TIME (LAMBDA NIL 
          (CLOCK 2)) (LAMBDA (X) (FQUOTIENT X 
          (TICKPS)))) FORM1 is the parameter being measured, FORM2 
          (optional) can be used to convert the value of FORM1 to some 
          other units, e.g. clock ticks to seconds.)


              (HELP (CONS X (QUOTE (NOT FOUND]
          [PUTD (QUOTE BRKDWN2)
                (OR [GETD (SETQ Y (PACK (LIST (QUOTE BRKDWN)
                                              BRKDWNTYPE]
                    (GETD (COMPILE2 Y (SUBST (CADR X)
                                             (QUOTE FORM)
                                             BRKDWNFN]

          (* * The function used for breaking the functions of 
          interest is BRKDWNNAME e.g. BRKDWNTIME, BRKDWNCONSES etc. 
          Its definition is created, if not already defined, by 
          substituting FORM1 for 'FORM' in BRKDWNFN and then 
          compiling. BRKDWNTIME is already defined in the system since 
          it is used so frequently.)


          (SETQ BRKDWNTERP (CADDR X))
          (SETQ BRKDWNLABEL BRKDWNTYPE)
          (SETQ BRKDWNTYPE NIL])

(BRKDWNTIME
  [NLAMBDA (BDEXP BDX BDCALLS BDN)
    (PROG (BDY BDZ)
          [COND
            [BDPTR (CLOSER (LOC BDPTR)
                           (IPLUS (OPENR (LOC BDPTR))
                                  (IDIFFERENCE
                                    (OPENR (LOC BDLST))
                                    (CLOSER (LOC BDLST)
                                            (LOC (ASSEMBLE NIL
                                                           (JSYS 316Q)
                                                           (SUB 1 , GCTIM]
            (T (CLOSER (LOC BDLST)
                       (LOC (ASSEMBLE NIL
                                      (JSYS 316Q)
                                      (SUB 1 , GCTIM]
          [CLOSER (LOC BDCALLS)
                  (ADD1 (OPENR (LOC BDCALLS]
          (SETQ BDZ BDPTR)
          (SETQ BDPTR BDX)
      LP  (SETQ BDY (EVAL BDEXP))
          (COND
            ((NEQ BDN 1)
              (SUB1VAR BDN)
              (GO LP)))
          (SETQ BDX BDPTR)
          (SETQ BDPTR BDZ)
          [CLOSER (LOC BDX)
                  (IPLUS (OPENR (LOC BDX))
                         (IDIFFERENCE (OPENR (LOC BDLST))
                                      (CLOSER (LOC BDLST)
                                              (LOC (ASSEMBLE NIL
                                                             (JSYS 316Q)
                                                             (SUB 1 , GCTIM]
          (RETURN BDY])

(BRKDWNCONSES
  [NLAMBDA (BDEXP BDX BDCALLS BDN)
    (PROG (BDY BDZ)
          (SETQ BDZ (CONSCOUNT))
          (COND
            [BDPTR (CLOSER (LOC BDPTR)
                           (IPLUS (OPENR (LOC BDPTR))
                                  (IDIFFERENCE (OPENR (LOC BDLST))
                                               (CLOSER (LOC BDLST)
                                                       BDZ]
            (T (CLOSER (LOC BDLST)
                       BDZ)))
          [CLOSER (LOC BDCALLS)
                  (ADD1 (OPENR (LOC BDCALLS]
          (SETQ BDZ BDPTR)
          (SETQ BDPTR BDX)
      LP  (SETQ BDY (EVAL BDEXP))
          (COND
            ((NEQ BDN 1)
              (SUB1VAR BDN)
              (GO LP)))
          (SETQ BDX BDPTR)
          (SETQ BDPTR BDZ)
          [CLOSER (LOC BDX)
                  (IPLUS (OPENR (LOC BDX))
                         (IDIFFERENCE (OPENR (LOC BDLST))
                                      (CLOSER (LOC BDLST)
                                              (CONSCOUNT]
          (RETURN BDY])

(RESULTS
  [LAMBDA NIL
    (RESETFORM (FLTFMT BRKDWNFLTFMT)
               (PROG (X TOT1 TOT2)
                     (CONSCOUNT 0)
                     (SETQ TOT1 (SETQ TOT2 0))
                     (LISPXPRIN1 (QUOTE "FUNCTIONS   ")
                                 T)
                     (LISPXPRIN1 BRKDWNLABEL T)
                     (LISPXPRIN1 (QUOTE "     # CALLS     PER CALL
")
                                 T)
                     [MAPC BRKDWNLST
                           (FUNCTION (LAMBDA (X)
                               (RESULTS1 (CAR X)
                                         [OPENR (LOC (CAR (SETQ X (CDR X]
                                         [OPENR (LOC (CAR (SETQ X (CDR X]
                                         (CADR X]
                     (RESULTS1 (QUOTE TOTAL)
                               TOT1 TOT2 1)
                     (RETURN NIL])

(RESULTS1
  [LAMBDA (NAME X NCALLS N)
    (PROG ((BRKDWNTERP BRKDWNTERP))
          (LISPXPRIN2 NAME T T)
          (LISPXTAB 12 NIL T)
          [COND
	    ((EQ N 1)
	      (SETQ TOT1 (FPLUS TOT1 X)))
	    (T (SETQ TOT1 (FPLUS TOT1 (SETQ X (FQUOTIENT X N]
          (LISPXPRIN2 [SETQ X (COND
			  (BRKDWNTERP (APPLY* BRKDWNTERP (FMINUS X)))
			  (T (IMINUS X]
		      T T)
          (LISPXTAB 25 NIL T)
          (SETQ TOT2 (IPLUS TOT2 NCALLS))
          (LISPXPRIN2 NCALLS T T)
          (LISPXTAB 35 NIL T)
          (LISPXPRIN2 (FQUOTIENT X NCALLS)
		      T T)
          (LISPXTERPRI T])
)
  [RPAQQ BRKDWNTYPES ((TIME (LOC (ASSEMBLE NIL (JSYS 206)
					   (SUB 1 , GCTIM)))
			    [LAMBDA (X)
				    (FQUOTIENT X 1000])
	  (CONSES (CONSCOUNT)
		  NIL)
	  (PAGEFAULTS (PAGEFAULTS)
		      NIL)
	  [BOXES (LOC (ASSEMBLE NIL (MOVE 1 , IBOXCN]
	  (FBOXES (LOC (ASSEMBLE NIL (MOVE 1 , FOBXCN]
  [RPAQQ BRKDWNFN
	 (NLAMBDA
	   (BDEXP BDX BDCALLS BDN)
	   (PROG (BDY BDZ)
		 (* This function is used as a template for creating functions 
		    for purposes of BREAKDOWN. The form to be measured is 
		    substituted for 'form' and the result then compiled. See 
		    definition of BRKDWNTIME.)
		 (COND
		   [(CAR (QUOTE BDPTR))
		    (CLOSER (LOC (CAR (QUOTE BDPTR)))
			    (IPLUS [OPENR (LOC (CAR (QUOTE BDPTR]
				   (IDIFFERENCE
				     [OPENR (LOC (CAR (QUOTE BDLST]
				     (CLOSER (LOC (CAR (QUOTE BDLST)))
					     FORM]
		   (T (CLOSER (LOC (CAR (QUOTE BDLST)))
			      FORM)))
		 [CLOSER (LOC BDCALLS)
			 (ADD1 (OPENR (LOC BDCALLS]
		 (SETQ BDZ (CAR (QUOTE BDPTR)))
		 (FRPLACA (QUOTE BDPTR)
			  BDX)
		 LP
		 (SETQ BDY (EVAL BDEXP))
		 (COND ((NEQ BDN 1)
			(SUB1VAR BDN)
			(GO LP)))
		 (SETQ BDX (CAR (QUOTE BDPTR)))
		 (FRPLACA (QUOTE BDPTR)
			  BDZ)
		 [CLOSER (LOC BDX)
			 (IPLUS (OPENR (LOC BDX))
				(IDIFFERENCE [OPENR (LOC (CAR (QUOTE BDLST]
					     (CLOSER
					       (LOC (CAR (QUOTE BDLST)))
					       FORM]
		 (RETURN BDY]
  (RPAQQ BRKDWNTYPE TIME)
  (RPAQ BRKDWNLST)
  (RPAQQ BRKDWNFLTFMT 15588667584)
  (SETQ BDLST (VAG (IPLUS (LOC (ARRAY 1 1))
			  2)))
  [RPAQQ BRKDWNBLOCKS ((NIL BRKDWNTIME BRKDWNCONSES (GLOBALVARS BDPTR BDLST)
			    RESULTS1 RESULTS (LINKFNS . T]
[DECLARE: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY
  (BLOCK: NIL BRKDWNTIME BRKDWNCONSES (GLOBALVARS BDPTR BDLST)
	  RESULTS1 RESULTS (LINKFNS . T))
]
[DECLARE: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS 
  (ADDTOVAR NLAMA BREAKDOWN)
  (ADDTOVAR NLAML BRKDWNCONSES BRKDWNTIME)
]
(DECLARE: DONTCOPY
  (FILEMAP (NIL (632 9547 (BREAKDOWN 644 . 4007) (BRKDWN1 4011 . 5444) (BRKDWNTIME
5448 . 6944) (BRKDWNCONSES 6948 . 8026) (RESULTS 8030 . 8929) (RESULTS1 8933
. 9544)))))
STOP
