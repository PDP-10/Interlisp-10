SEARCH STENEX
;
;
;
;	SYSIN is a program for executing Lisp sysouts.
;	It asks for a sysout file name and then trys to
;	find the proper Lisp system. If it finds
;	a compatible system it merges the two files and
;	runs it.
;	If it does not find a compatible system it askes
;	the user for the proper name.
;	Once SYSIN has a compatible system, it writes its name
;	onto the sysout so that it won't always have to search or
;	ask the user.
;	The systems which SYSIN looks at are all existing versions of
;	<SUBSYS>LISP.SAV, <SUBSYS>LISPX.SAV, <NEWLISP>*.SAV,
;	<TEITELMAN>*.SAV, <LISP>*.SAV, and *.SAV (i.e.,
;	the current connected directory).
;
;

P=17
PAGE=10000

START:	RESET
	MOVE	P,[IOWD 8,STACK]
;
;	GET SYSOUT JFN AND SYSDAT
;
	HRROI	3,[ASCIZ/Sysout filename: /]
	MOVEI	4,E
	PUSHJ	P,GETJFN
	MOVEM	1,SYSJFN	;SAVE JFN
	MOVEM	2,SYSDAT#	;SAVE SYSDAT
	MOVE	2,[440000,,202000]	;OPEN FILE AGAIN
	OPENF
	 JRST	SEARCH
	MOVEI	2,2700		;READ ITS DADDY'S NAME
	SFPTR
	 JFCL
	MOVE	2,[POINT 36,BUFF]
	SETZ	3,
	SIN
	HRLI	1,400000	;CLOSE IT BUT DONT RELEASE
	CLOSF
	 JFCL
	MOVE	1,[100121,,-3]	;GET THE JFN OF THE DADDY
	HRROI	2,BUFF
	GTJFN
	 JRST	SEARCH		;NO FILE, OR ITS BEEN MOVED
	MOVEM	1,SAVJFN	;SAVE THE JFN
	SETZM	CHGFLG
	PUSHJ	P,MAPJFN	;IS DADDY REALLY COMPATBLE?
	 JRST	GOTIT2		;YES
;
;	FIND A COMPATIBLE SAV FILE
;
SEARCH:	SETOM	CHGFLG		;ALWAYS PRINT
	MOVE	5,[FSTSAV-LSTSAV,,FSTSAV]	;LOOP COUNTER
LOOP:	MOVE	2,(5)		;GET STRING POINTER
	MOVSI	1,100121
	GTJFN			;GET THE JFN LIST
	 JRST	ELOOP
	MOVEM	1,SAVJFN
	PUSHJ	P,MAPJFN	;FIND A COMPATIBLE
	 JRST	GOTIT		;ONE MATCHES
ELOOP:	AOBJN	5,LOOP		;TRY NEXT FILE LIST
;
;	ASK USER FOR FILE
;
	SETZM	CHGFLG		;ASSUME WE WONT HAVE TO PRINT
	HRROI	1,[ASCIZ/Makesys filename: /]
	PSOUT
RETRY2:	HRROI	3,[ASCIZ/Makesys filename: /]
	MOVEI	4,E1		;BLOCK TO ALLOW *
	PUSHJ	P,GETJFN
	MOVEM	1,SAVJFN
	PUSHJ	P,MPJFN2	;CHECK DATES
	 JRST	GOTIT
	HRROI	1,[ASCIZ/The files are not compatible./]
	PSOUT
	HRROI	1,[ASCIZ/
Makesys filename: /]
	PSOUT
	PUSHJ	P,SLEEP
	JRST	RETRY2
;
;	WE HAVE A COMPATIBLE FILE, RUN IT WITH THE SYSOUT
;
GOTIT2:	MOVNS	CHGFLG#		;FLAG FOR NO WRITE
GOTIT:	SKIPN	CHGFLG		;PRINT IT?
	JRST	GOT3		;NO
	HRROI	1,[ASCIZ /from /] ;YES
	PSOUT
	MOVEI	1,101
	HRRZ	2,SAVJFN
	SETZ	3,
	JFNS
	MOVEI	1,37
	PBOUT
GOT3:	SKIPL	1,CHGFLG	;WRITE NEW NAMES?
	JRST	GOT2
	HRRZ	1,SYSJFN	;YES
	MOVE	2,[440000,,302000] ;OPEN THAWED.
	OPENF
	 JRST	GOT2		;CAN'T OPEN TO WRITE
	HRLZI	1,0(1)		;GET THE MAGIC PAGE
	HRRI	1,2		;IE, PAGE 2
	MOVE	2,[400000,,PAGE/1000]
	MOVSI	3,140000		;ALLOW DIRECT WRITE
	PMAP
	HRRZ	2,SAVJFN	;WRITE DADDY NAME INTO BUFFER
	HRROI	1,PAGE+700
	MOVE	3,[111100,,1]
	JFNS
	SETZM	1(1)		;MAKE SURE IT ENDS IN A ZERO WORD
	SETO	1,
	MOVE	2,[400000,,PAGE/1000]
	PMAP			;DELETE THE PAGE
	HRRZ	1,SYSJFN
	HRLI	1,400000	;CLOSE SYSOUT
	CLOSF
	 JFCL
GOT2:	MOVE	1,SAVJFN
GOTIT1:	HRLI	1,400000
	MOVEM	1,SAVJFN	;SAVE THE SSAVE JFN
	HLLM	1,SYSJFN
	MOVE	1,[SIXBIT /LISP/]
	SETNM			;SET THE NAME TO "LISP"
	MOVE	1,[STRUP,,3]	;MOVE START UP CODE TO ACS
	BLT	1,17
	JRST	3		;GO START

STRUP:	MOVE	1,16		;GET THE SAV FILE
	GET
	MOVE	1,15		;GET THE SYS FILE
	GET
	PUSHJ	P,@1063		; CALL SETINT
	MOVE	1,1063
	PUSHJ	P,22(1)		; CALL SETTRP
	MOVE	1,15
	MOVE	2,1052		;USE IFSET TO GET THE FUNNY ENTRY POINT
	JRST	-44(2)		;... AND JUMP TO IT.
SYSJFN:	0
SAVJFN:	0
	IOWD	3,600		;A TEMPORARY STACK FOR INITIALIZATION.
;
;
;	GETDAT - GETS THE SYSDAT OF A FILE
;
;	ACCEPTS IN 1: JFN OF FILE
;
;	RETURNS +1: FAILURE, ERROR CODE IN 1
;		+2: SUCCESS, JFN IN 1, SYSDAT IN 2
;
GETDAT:	PUSH	P,1
	HRLI	1,0		;MAKE SURE THERE ARE NO * BITS
	MOVE	2,[440000,,202000]
	OPENF			;OPEN THE FILE
	 JRST	GETDT2		;ERROR - RETURN
	MOVEI	3,2777		;GET THE SYSDAT
	RIN
	PUSH	P,2
	HRLI	1,400000	;KEEP IT
	CLOSF			;CLOSE THE FILE
	 JFCL			;IGNORE ERROR FOR NOW
	AOS	-2(P)		;SKIP ON GOOD RETURN
	POP	P,2		;GET SYSDAT
GETDT2:	POP	P,1		;RESTORE JFN
	POPJ	P,

;
;
;	GETJFN - GETS A FILE NAME FROM TTY: AND GETS ITS SYSDAT
;
;	ACCEPTS IN  3: A PROMPT STRING
;		    4: POINTER TO THE LONG BLOCK FOR GTJFN
;
;	RETURNS +1 ALWAYS, JFN IN 1, SYSDAT IN 2
;
GETJFN:	PBIN			;TRIM OFF LEADING BLANKS
	CAIN	1," "
	JRST	.-2
	CAIE	1,37		;EOL?
	JRST	GET1		;NO
PROMPT:	MOVEI	1,(3)		;YES - PRINT THE PROMPT
	PSOUT
	PUSHJ	P,SLEEP
	JRST	GETJFN		;RETRY
GET1:	MOVEI	1,100		;BACK UP TTY PTR.
	BKJFN
	 JFCL
	SETZ	2,		;GET THE JFN
	MOVEI	1,0(4)
	GTJFN
	 JRST	QUES		;ERROR
	MOVE	5,1		;SAVE THE JFN
	TLNE	1,760000	;ANY STARS (EXCEPT VERSION)?
	SETOM	CHGFLG		;YES, SET PRINT FLAG
	MOVEI	1,100
	BKJFN			;BACKUP TO GET BREAK CHAR.
	 JFCL
	PBIN
	CAIN	1,33		;ENDED WITH ESC?
	PBIN			;YES - IGNORE IT
	CAIN	1," "		;IGNORE TRAILING BLANKS
	JRST	.-2
	CAIE	1,37		;MUST END WITH EOL
	JRST	PROMPT
	MOVE	1,5		;RESTORE JFN
	PUSH	P,3
	PUSHJ	P,GETDAT	;GET THE SYSDAT
	 JRST	QUES-1		;ERROR
	SUB	P,[1,,1]
	POPJ	P,

	POP	P,3
QUES:	MOVEI	1,100		;CHECK LAST CHAR.
	BKJFN
	 JFCL
	PBIN
	CAIN	1,177		;WAS IT A RUBOUT
	SKIPA	1,[-1,,[ASCIZ/ XXX
/]]				;YES
	HRROI	1,[ASCIZ/ ?
/]				;NO
	PSOUT
	JRST	PROMPT

;
;	STEP THRU A GROUP JFN AND TEST IF ONE HAS A COMPATIBLE SYSDAT
;		SKIP IF ONE IS FOUND
;
	SETOM	CHGFLG
MAPJFN:	PUSHJ	P,GETDAT	;GET THE SYSDAT
	 JRST	EMAP
MPJFN2:	CAMN	2,SYSDAT	;SAME SYSDAT?
	POPJ	P,		;YES
EMAP:	MOVE	1,SAVJFN	;NO - STEP TO NEXT JFN IN LIST
	GNJFN
	AOSA	0(P)		;LIST IS EMPTY
	JRST	MAPJFN-1	;TRY NEXT ONE IN GROUP
	POPJ	P,

;	WAIT FOR OUTPUT AND CLEAR INPUT BUFFER

SLEEP:	MOVEI	1,101		;WAIT FOR OUTPUT TO FINISH
	DOBE
	MOVEI	1,100		;CLEAR INPUT BUFFER
	CFIBF
	POPJ	P,

E:	100000,,0
	100,,101
	BLOCK	7

E1:	100120,,-3
	100,,101
	BLOCK	2
	-1,,[ASCIZ /*/]
	-1,,[ASCIZ /SAV/]
	BLOCK	3

STACK:	BLOCK	8

FSTSAV:	-1,,[ASCIZ/<SUBSYS>LISP.SAV;*/]
	-1,,[ASCIZ/<SUBSYS>LISPX.SAV;*/]
	-1,,[ASCIZ/<NEWLISP>*.SAV;*/]
	-1,,[ASCIZ/<TEITELMAN>*.SAV;*/]
	-1,,[ASCIZ/<LISP>*.SAV;*/]
	-1,,[ASCIZ/*.SAV;*/]
LSTSAV:

BUFF:	BLOCK	77

	END	START
