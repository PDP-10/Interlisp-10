(FILECREATED "19-SEP-74 22:17:26" BOOTSTRAP.;22 9299   

     changes to:  BOOTSTRAPCOMS

     previous date: "19-SEP-74 12:18:38" BOOTSTRAP.;21)


  (LISPXPRINT (QUOTE BOOTSTRAPCOMS)
	      T T)
  [RPAQQ BOOTSTRAPCOMS
	 ((FNS * BOOTSTRAPFNS)
	  [P (COND ((EQ (CAR (QUOTE DWIMFLG))
			(QUOTE NOBIND))
		    [MAPC (QUOTE ((/PUTD . PUTD)
				  (/PUT . PUT)
				  (LISPXPRIN1 . PRIN1)
				  (LISPXPRIN2 . PRIN2)
				  (LISPXPRINT . PRINT)
				  (LISPXTERPRI . TERPRI)
				  (LISPXSPACES . SPACES)))
			  (FUNCTION (LAMBDA (X)
					    (PUTD (CAR X)
						  (GETD (CDR X]
		    (* Included so i can use lispxprinting functions in my 
		       files, i.e. in bootstrap and wtmisc. the appropriate 
		       places where they should be used in basic and misc are 
		       handled by a changename in wtmisc.)
		    (PUTD (QUOTE LISPXPRIN1)
			  (GETD (QUOTE PRIN1)))
		    [PUTDQ STRPOS (LAMBDA
			     (X Y START SKIP ANCHOR TAIL)
			     [COND [(LITATOM X)
				    (SETQ X (CDR (VAG (IPLUS (LOC X)
							     2]
				   ((NULL (STRINGP X))
				    (SETQ X (MKSTRING X]
			     [COND ((STRINGP Y))
				   [(LITATOM Y)
				    (SETQ Y (CDR (VAG (IPLUS (LOC Y)
							     2]
				   (T (SETQ Y (MKSTRING Y]
			     [COND (SKIP (SETQ SKIP (NTHCHAR SKIP 1]
			     (COND [START (COND ((MINUSP START)
						 (SETQ START
						       (IPLUS START
							      (NCHARS Y)
							      1]
				   (T (SETQ START 1)))
			     (SETQ Y (SUBSTRING Y START))
			     (PROG ((N START)
				    W X1 Y1)
				   L2
				   (SETQ X1 (SUBSTRING X 1))
				   (SETQ Y1 (SUBSTRING Y 1))
				   LP
				   (COND [(SETQ W (GNC X1))
					  (COND ((EQ W (GNC Y1))
						 (GO LP))
						((EQ W SKIP)
						 (GO LP))
						(T (GO NX]
					 (TAIL (RETURN (IPLUS (NCHARS X)
							      N)))
					 (T (RETURN N)))
				   NX
				   (COND (ANCHOR (RETURN)))
				   (COND ((GNC Y)
					  (SETQ N (ADD1 N))
					  (GO L2))
					 (T (RETURN]
		    (RPAQQ NOTCOMPILEDFILES NIL)
		    (RPAQQ DFNFLG T)
		    (RPAQQ PRETTYTYPELST NIL)
		    (RPAQQ BUILDMAPFLG NIL)
		    (RPAQQ USEMAPFLG NIL)
		    (RPAQQ RESETVARSLST NIL)
		    (RPAQQ DWIMFLG NIL)
		    (RPAQQ LISPXHIST NIL)
		    (RPAQQ FILEPKGFLG NIL)
		    (RPAQQ LISPXPRINTFLG T)
		    (RPAQQ PRETTYHEADER "FILE CREATED ")
		    (SETQ FILERDTBL (COPYREADTABLE T))
		    (SETQ EDITRDTBL (COPYREADTABLE T))
		    (RPAQQ NOSWAPFLG T]
	  (DECLARE: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS
		    (ADDVARS (NLAMA DECLARE: FILECREATED)
			     (NLAML FILEMAP]
  (RPAQQ BOOTSTRAPFNS (MOVD NCONC1 PUT NAMEFIELD FILECREATED FILEMAP DECLARE:))
(DEFINEQ

(MOVD
  [LAMBDA (FROM TO COPYFLG)
    [PUTD TO (COND
	       (COPYFLG (COPY (GETD FROM)))
	       (T (GETD FROM]
    (AND FILEPKGFLG (NEWFILE? TO)) TO])

(NCONC1
  [LAMBDA (LST X)
    (NCONC LST (FRPLACD (CONS X LST])

(PUT
  [LAMBDA (ATM PROP VAL)

          (* Included because it must be defined before the MOVD's in 
          BOOTSTRAPCOMS that initialize /PUT and /REMPROP are 
          executed.)


    [COND
      [(NULL ATM)
	(ERRORX (LIST 7 (LIST ATM PROP]
      ((NOT (LITATOM ATM))
	(ERRORX (LIST 14 ATM]
    (PROG ((X ATM))
      LOOP[COND
	    ((NLISTP (CDR X))
	      (FRPLACD X (LIST PROP VAL)))
	    ((EQ (CADR X)
		 PROP)
	      (FRPLACA (CDDR X)
		       VAL))
	    ((SETQ X (CDDR X))
	      (GO LOOP))
	    (T (FRPLACD ATM (CONS PROP (CONS VAL (CDR ATM]
          (RETURN VAL])

(NAMEFIELD
  [LAMBDA (FILE SUFFIXFLG)                      (* IF SUFFIXFLG is T, returns 
                                                name and suffix field, otherwise
                                                just NAMEFIELD)
    (PROG (TEM)
          [COND
	    ((AND (EQ (NTHCHAR FILE 1)
		      (QUOTE <))
		  (SETQ TEM (STRPOS (QUOTE >)
				    FILE)))     (* Eliminates directory field.)
	      (SETQ FILE (MKATOM (SUBSTRING FILE (ADD1 TEM]
          [COND
	    ([AND [OR SUFFIXFLG (NULL (SETQ TEM (STRPOS (QUOTE %.)
							FILE]
		  (NULL (SETQ TEM (STRPOS (QUOTE ;)
					  FILE]
	      (RETURN FILE))
	    ((EQ (NTHCHAR FILE (SUB1 TEM))
		 (QUOTE %.))                    (* Eliminates NULL suffix.)
	      (SETQ TEM (SUB1 TEM]
          (RETURN (MKATOM (SUBSTRING FILE 1 (SUB1 TEM])

(FILECREATED
  [NLAMBDA X
    (PROG ((FILEDATE (CAR X))
	   (FILE (CADR X)))
          (SETQ FILECREATEDLST (NCONC1 FILECREATEDLST X))

          (* I (W.T>) have tried to keep FILECREATED not dependent on 
          the fact that it was evaluated while being loaded.
          However, the same FILECREATED expressionhas to act 
          differently when it appears in a compiled file than symbolic 
          file in latter case it can use 
          (INPUT) for name of file, thereby obtaning directory name if 
          different. in former case, it must obtain name of file from 
          the expressionitself. I suppose we could always dump out the 
          directory name but this wold lose if user copied it to a 
          different directory, and or version number.
          The best thing is to go with INPUT for symbolic files, and 
          go the best you can on compiled files, i.e. get version 
          number from the first filecreated expression.
          Thus simply putting the FILECREATED expression on FILEDATES 
          as a string initially and then, if filepkgflg is on, doing 
          the full shtick when addfile is called seems best.)


          (COND
	    (PRETTYHEADER 

          (* Presumably if user sets prettyheader to NIL, he doesnt 
          want to see any file created messages, even those frm 
          compiled files.)


			  (LISPXPRIN1 (COND
					((STRINGP FILE)
                                                (* Used for printing COMPILED ON
                                                message.)
					  FILE)
					((LISTP FILE)

          (* New. also used for printing COMPILED ON message.
          CDR is a list of files that were compiled.)


					  (CAR FILE))
					(T PRETTYHEADER))
				      T)
			  (LISPXPRIN1 FILEDATE T)
			  (LISPXTERPRI T)))
          (COND
	    ((AND FILE (ATOM FILE))

          (* This is just temporary, primarily for keeping dates of 
          system files which are loaded with FILEPKGFLG=NIL.
          The real setting up oo file property lists is done when 
          ADDFILE is called.)


	      (/PUT (NAMEFIELD FILE)
		    (QUOTE FILEDATES)
		    FILEDATE])

(FILEMAP
  [NLAMBDA (FILEMAP)
    (PROG (FL)
          (/PUT (NAMEFIELD (SETQ FL (INPUT))
			   T)
		(QUOTE FILEMAP)
		(CONS FL FILEMAP])

(DECLARE:
  [NLAMBDA X
    (PROG ((EVALFLG T))
          (MAPC X (FUNCTION (LAMBDA (X)
		    (COND
		      ((LISTP X)
			(AND EVALFLG (EVAL X)))
		      (T (SELECTQ X
				  ((EVAL@LOAD DOEVAL@LOAD)
				    (SETQ EVALFLG T))
				  (DONTEVAL@LOAD (SETQ EVALFLG NIL))
				  NIL])
)
  (COND ((EQ (CAR (QUOTE DWIMFLG))
	     (QUOTE NOBIND))
	 [MAPC (QUOTE ((/PUTD . PUTD)
		       (/PUT . PUT)
		       (LISPXPRIN1 . PRIN1)
		       (LISPXPRIN2 . PRIN2)
		       (LISPXPRINT . PRINT)
		       (LISPXTERPRI . TERPRI)
		       (LISPXSPACES . SPACES)))
	       (FUNCTION (LAMBDA (X)
				 (PUTD (CAR X)
				       (GETD (CDR X]
	 (* Included so i can use lispxprinting functions in my files, i.e. in 
	    bootstrap and wtmisc. the appropriate places where they should be 
	    used in basic and misc are handled by a changename in wtmisc.)
	 (PUTD (QUOTE LISPXPRIN1)
	       (GETD (QUOTE PRIN1)))
	 [PUTDQ STRPOS (LAMBDA (X Y START SKIP ANCHOR TAIL)
			       [COND [(LITATOM X)
				      (SETQ X
					    (CDR (VAG (IPLUS (LOC X)
							     2]
				     ((NULL (STRINGP X))
				      (SETQ X (MKSTRING X]
			       [COND ((STRINGP Y))
				     [(LITATOM Y)
				      (SETQ Y
					    (CDR (VAG (IPLUS (LOC Y)
							     2]
				     (T (SETQ Y (MKSTRING Y]
			       [COND (SKIP (SETQ SKIP (NTHCHAR SKIP 1]
			       (COND [START (COND ((MINUSP START)
						   (SETQ START
							 (IPLUS START
								(NCHARS Y)
								1]
				     (T (SETQ START 1)))
			       (SETQ Y (SUBSTRING Y START))
			       (PROG ((N START)
				      W X1 Y1)
				     L2
				     (SETQ X1 (SUBSTRING X 1))
				     (SETQ Y1 (SUBSTRING Y 1))
				     LP
				     (COND [(SETQ W (GNC X1))
					    (COND ((EQ W (GNC Y1))
						   (GO LP))
						  ((EQ W SKIP)
						   (GO LP))
						  (T (GO NX]
					   (TAIL (RETURN (IPLUS (NCHARS X)
								N)))
					   (T (RETURN N)))
				     NX
				     (COND (ANCHOR (RETURN)))
				     (COND ((GNC Y)
					    (SETQ N (ADD1 N))
					    (GO L2))
					   (T (RETURN]
	 (RPAQQ NOTCOMPILEDFILES NIL)
	 (RPAQQ DFNFLG T)
	 (RPAQQ PRETTYTYPELST NIL)
	 (RPAQQ BUILDMAPFLG NIL)
	 (RPAQQ USEMAPFLG NIL)
	 (RPAQQ RESETVARSLST NIL)
	 (RPAQQ DWIMFLG NIL)
	 (RPAQQ LISPXHIST NIL)
	 (RPAQQ FILEPKGFLG NIL)
	 (RPAQQ LISPXPRINTFLG T)
	 (RPAQQ PRETTYHEADER "FILE CREATED ")
	 (SETQ FILERDTBL (COPYREADTABLE T))
	 (SETQ EDITRDTBL (COPYREADTABLE T))
	 (RPAQQ NOSWAPFLG T)))
[DECLARE: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS 
  (ADDTOVAR NLAMA DECLARE: FILECREATED)
  (ADDTOVAR NLAML FILEMAP)
]
(DECLARE: DONTCOPY
  (FILEMAP (NIL (2642 6978 (MOVD 2654 . 2811) (NCONC1 2815 . 2880) (PUT 2884
. 3487) (NAMEFIELD 3491 . 4309) (FILECREATED 4313 . 6537) (FILEMAP 6541 .
6684) (DECLARE: 6688 . 6975)))))
STOP
