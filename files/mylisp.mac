SEARCH STENEX

P=17

START:	RESET
	MOVE	P,[IOWD 8,STACK]
	MOVSI	1,100001	;GET THE SUBSYSTEM NAME
	HRROI	2,SYSNAM
	GTJFN
	 JRST	FAIL		;NOT THERE
	HRLI	1,400000
	MOVEM	1,SAVJFN
	PUSHJ	P,GETDAT	;GET IT'S SYSDAT
	 JRST	FAIL2		;CAN'T OPEN TO READ
	MOVEM	2,SAVDAT#
	GJINF			;GET LOGIN DIRECTORY NUMBER
	MOVE	2,1
	MOVE	1,[POINT 7,BUFF,6]
	DIRST			;CONVERT TO USER NAME
	 JFCL
	HRROI	2,MYLISP	;BUILD THE REST OF THE USER'S FILE NAME
	SETZ	3,
	SOUT
	HRRZ	2,SAVJFN
	MOVSI	3,10
	JFNS			;... AND TACK ON SYSTEM VERSION NUMBER
	HRROI	2,[ASCIZ/.SYS/]
	SETZ	3,
	SOUT			;EXTENSION OF "SYS"
	MOVSI	1,100001	;GET THE SYSOUT JFN
	HRROI	2,BUFF
	GTJFN
	 JRST	RUN		;NOT THERE, RUN SUBSYS
	PUSHJ	P,GETDAT	;GET SYSDAT
	 JRST	FAIL3		;CAN'T OPEN
	HRLI	1,400000
	MOVEM	1,SYSJFN
	CAME	2,SAVDAT	;DO DATES MATCH?
	JRST	RUN		;NO
;
;	WE HAVE A COMPATIBLE FILE, RUN IT WITH THE SYSOUT
;
	MOVE	1,SYSNM
	SETNM			;SET THE NAME TO "LISP"
	MOVE	1,[STRUP,,3]	;MOVE START UP CODE TO ACS
	BLT	1,17
	JRST	3		;GO START

STRUP:	MOVE	1,16		;GET THE SAV FILE
	GET
	MOVE	1,15		;GET THE SYS FILE
	GET
	PUSHJ	P,@1063		; CALL SETINT
	MOVE	1,1063
	PUSHJ	P,22(1)		; CALL SETTRP
	MOVE	1,15
	MOVE	2,1052		;USE IFSET TO GET THE FUNNY ENTRY POINT
	JRST	-44(2)		;... AND JUMP TO IT.
SYSJFN:	0
SAVJFN:	0
	IOWD	3,600		;A TEMPORARY STACK FOR INITIALIZATION.

;
;	RUN THE SUBSYS
;
RUN:	MOVE	1,SYSNM
	SETNM			;SET THE NAME
	MOVE	1,[RUNCOD,,3]
	BLT	1,7
	JRST	3

RUNCOD:	MOVE	1,SAVJFN
	GET
	HLRZ	1,1
	GEVEC
	JRST	(2)

;
;
;	GETDAT - GETS THE SYSDAT OF A FILE
;
;	ACCEPTS IN 1: JFN OF FILE
;
;	RETURNS +1: FAILURE, ERROR CODE IN 1
;		+2: SUCCESS, JFN IN 1, SYSDAT IN 2
;
GETDAT:	HRLI	1,0
	MOVE	2,[440000,,202000]
	OPENF			;OPEN THE FILE
	 POPJ	P,		;ERROR - RETURN
	MOVEI	3,2777		;GET THE SYSDAT
	RIN
	PUSH	P,1
	PUSH	P,2
	HRLI	1,400000	;KEEP IT
	CLOSF			;CLOSE THE FILE
	 JFCL			;IGNORE ERROR FOR NOW
	POP	P,2		;GET SYSDAT
	POP	P,1		;RESTORE JFN
	AOS	0(P)		;SKIP ON GOOD RETURN
	POPJ	P,

FAIL:	HRROI	1,[ASCIZ/
Cannot find /]
	PSOUT
	HRROI	1,SYSNAM
FA3:	PSOUT
	RESET
	HALTF

FAIL2:	HRROI	1,[ASCIZ/
Cannot open /]
	JRST	FAIL+1

FAIL3:	HRROI	1,[ASCIZ/
Cannot open /]
	PSOUT
	HRROI	1,BUFF
	JRST	FA3

SYSNM:	SIXBIT	/LISP/
SYSNAM:	ASCIZ	/<SUBSYS>LISP.SAV;0/
MYLISP:	ASCIZ	/>MYLISP#/

STACK:	BLOCK	8
BUFF:	ASCIZ	/</
	BLOCK	67

	END	START
