(FILECREATED "15-OCT-74 22:24:19" EDITA.;1 22179  

     changes to:  EDITA

     previous date: "18-SEP-74 14:08:30" <NEWLISP>EDITA.;6)


  (LISPXPRINT (QUOTE EDITACOMS)
	      T T)
  [RPAQQ EDITACOMS ((FNS * EDITAFNS)
	  (VARS (SYMLST)
		(USERSYMS))
	  (DECLARE: DOEVAL@COMPILE DONTCOPY (PROP BLKLIBRARYDEF BETWEEN)
		    )
	  (BLOCKS * EDITABLOCKS)
	  (P (AND (NULL (GETD (QUOTE LOADUP)))
		  (EDITAINIT]
  (RPAQQ EDITAFNS
	 (EDITAINIT EDITA EDITA1 EDITASYM EDITAREED EDITAPRINT EDITAW 
		    EDITA2 EDITA3 EDITA4 EDITALIT DECODE EDITADR 
		    EDITAOPENR EDITACLOSER BETWEEN))
(DEFINEQ

(EDITAINIT
  [LAMBDA NIL

          (* EDITAINIT is called automatically when EDITA is 
          loaded except when it is loaded as part of the 
          loadup process. In this case, EDITAINIT is postponed 
          until the first time EDITA is used.)


    (AND (EQ (CAR (QUOTE OPCODES))
	     (QUOTE NOBIND))
	 (LOAD (QUOTE <LISP>OPCODES)))          (* In case user tries to
                                                load EDITA into a system
                                                without compiler.)
    (SETQ OPCODEARRAY (ARRAY 448))
    [MAPC OPCODES (FUNCTION (LAMBDA (X)
	      (SETA OPCODEARRAY (RSH (GETP X (QUOTE OPD))
				     9)
		    X]
    [SETQ SYMLST (MAPCAR (CDDDR COREVALS)
			 (FUNCTION (LAMBDA (X)
			     (CONS (GETP X (QUOTE COREVAL))
				   X]
    [SETSEPR (QUOTE (% ))
	     NIL
	     (SETQ EDITARDTBL (COPYREADTABLE (QUOTE ORIG]
    (SETBRK (QUOTE (%( %) , %
 %
 / : %	 = %[ %] ? ^ ' @ %" ;))
	    NIL EDITARDTBL])

(EDITA
  [LAMBDA (EDITARRY COMS)

          (* NOTE: This file contains machine DEPENDENT code, 
          namely the use of LOC and VAG for directly 
          referencing arrays. Also, since it decodes machine 
          instructions for the PDP-10, it probably would need 
          rework to be useful for another machine.)



          (* Addressing conventions: the variable.
          references the current register.
          It is an integer smaller than the size of the 
          function or ARRAY being edited.
          For functions,. Plus the value of GETD is the 
          address of the current register.
          For arrays,. Corresponds to ELT so.
          + arraypointer + 1 is the address of the current 
          register. EDITADR, EDITAOPENR, and EDITACLOSER all 
          know about this addressing convention.)



          (* If EDITA encounters an address not inside the 
          boundaries of the block being edited 
          (corresponds to minus 2 to $ for functions, minus 1 
          to $ for arrays) it see to see if the address is 
          inside of the ABSOLUTE boundaries of the block, i.e. 
          ORG minus 2 to ORG plus $. If so, the address is 
          converted to the corresponding relative address.
          Otherwise, it is taken as an absolute address.
          In this case, the user may open the register examine 
          its contents, but may not store into it.)


    (COND
      ((EQ (CAR (QUOTE OPCODEARRAY))
	   (QUOTE NOBIND))                      (* First time)
	(EDITAINIT)))
    (RESETLST
      (PROG (DEF ANYINPUT? Q U OPEN
	       FROM MASK %. BOXED LITS ORG $ #VARS HELPCLOCK LEFTX 
		    RIGHTX (FILE T)
		    LEFTFLAG LEFTHALF)
	TOP [COND
	      ((ARRAYP EDITARRY))
	      ((SWPARRAYP EDITARRY))
	      ([CCODEP (SETQ U (GETD (FNCHECK EDITARRY]
		(SETQ EDITARRY U)
		(SETQ DEF T))
	      (T (HELP (QUOTE (NOT EDITABLE]
	    [SETQ ORG (COND
		(DEF U)
		((SWPARRAYP EDITARRY)
		  EDITARRY)
		(T (VAG (IPLUS 2 (LOC EDITARRY]
	    [SETQ BOXED (SETQ LITS (COND
		  (DEF (FIRSTLIT ORG))
		  (T (SUB1 (LOC (FNOPENRA ORG -1]
                                                (* Value of. 
                                                Corresponding to first 
                                                literal.)
	    [SETQ $ (COND
		(DEF (SUB1 (LASTLIT+1 ORG)))
		(T (IPLUS -2 (LOC (FNOPENRA ORG -2]

          (* Value of. Corresponding to last word in ARRAY, or 
          last literal in function.)


	    [COND
	      (DEF 

          (* Number of arguments plus number of free 
          variables. Used for frefs and vrefs.)


		   (SETQ #VARS (IPLUS (LOC (FNOPENRD ORG 1))
				      (LOC (FNOPENRD ORG 2]
	    (SETQ %. (COND
		(DEF 0)
		(T 1)))
	    (SETQ U 0)
	    (PRINT (QUOTE EDIT)
		   T T)
	    (RESETSAVE (INPUT T))
	    (RESETSAVE (CONTROL T))
	LP  (COND
	      ([NULL (NLSETQ (EDITA1 (COND
				       [COMS (PROG1 (CAR COMS)
						    (SETQ COMS
						      (CDR COMS]
				       (T (EDITAREED]
		(PRIN1 (QUOTE "  ?
")
		       T)
		(SETQ U 0)
		[SETQ COMS (SETQ OPEN (SETQ FROM (SETQ ANYINPUT?]
		(CLEARBUF T)))
	    (GO LP])

(EDITA1
  [LAMBDA (EDITAX)
    (PROG NIL
      TOP (RETURN
	    (SELECTQ EDITAX
		     (OK (TERPRI T)
			 (CONTROL)
			 (RETFROM (QUOTE EDITA)
				  EDITARRY))
		     (%
 (COND
   ((AND (NULL (EDITACLOSER U))
	 ANYINPUT?)                             (* No register open and 
                                                something was typed => 
                                                type its value.)
     (PRINT (SETQ Q U)
	    T T)))
 (SETQ ANYINPUT? NIL)
 (SETQ U 0)
 (SETQ FROM NIL))
		     (=(RADIX (PROG1 (RADIX -8)
				     (PRIN2 (SETQ Q (COND
						((NULL OPEN)
						  U)
						(ANYINPUT? U)
						(T Q)))
					    T T)))
		       (SPACES 3 T)
		       (SETQ ANYINPUT? NIL)
		       (SETQ U 0))
		     ('(SETQ EDITAX (EDITAREED))
		       (COND
			 [(NUMBERP EDITAX)
			   (AND (SMALLP EDITAX)
				(SETQ EDITAX
				  (IPLUS (GETP (QUOTE ASZ)
					       (QUOTE COREVAL))
					 EDITAX]
			 ((NULL (SETQ EDITAX (EDITALIT EDITAX)))
			   (ERROR!)))
		       (GO TOP))
		     (? (SPACES 3 T)
			(SETQ ANYINPUT? NIL)
			(SETQ U 0)
			(SETQ LEFTFLAG NIL))
		     (^(COND
			 ((EQ %. (COND
				(DEF -2)
				(T -1)))
			   (PRINT (QUOTE (AT BEGINNING OF BLOCK))
				  T T)
			   (ERROR!)))
		       (EDITA2 (SUB1 %.))
		       (GO TOP))
		     (/(COND
			 ((NUMBERP FROM)
			   (PROG NIL
			     LP  (SPACES 3 FILE)
			         (SETQ EDITAX (EDITAOPENR FROM EDITAX))
			         (COND
				   ((ILESSP FROM BOXED)
				     (DECODE EDITAX FILE))
				   (T (EDITAPRINT EDITAX FILE)))
			         (TERPRI FILE)
			         (COND
				   ((IGREATERP (SETN
						  FROM (ADD1 FROM))
					       U)
				     (SETQ %. U)
				     (SETQ FROM NIL)
				     (TERPRI T)
				     (RETURN)))
			         (PRIN2 (EDITASYM FROM)
					FILE T)
			         (PRIN2 (QUOTE /)
					FILE T)
			         (GO LP)))
			 (T [SETQ %. (EDITADR (COND
						((NULL OPEN)
						  (SETQ OPEN T)
						  U)
						(ANYINPUT? U)
						(T Q]
			    (SPACES 3 T)
			    [COND
			      ((OR (ILESSP %. BOXED)
				   (IGREATERP %. $))
				(DECODE (SETQ Q (EDITAOPENR %.))
					T))
			      (T (EDITAPRINT (EDITAOPENR %.]
			    (SPACES 3 T)))
		       (SETQ ANYINPUT? NIL)
		       (SETQ U 0))
		     (%	 [SETQ Q (COND
			      [(NULL OPEN)
				(EDITAOPENR (SETQ %. (EDITADR %.]
			      (ANYINPUT? (EDITADR U))
			      (T (EDITADR Q]
			  (COND
			    ((OR (ILESSP %. BOXED)
				 (NUMBERP Q))
			      (EDITA2 Q))
			    (T (TERPRI T)
			       (EDITA (COND
					((LISTP Q)
					  (CAR Q))
					(T Q)))
			       (RETURN)))
			  (GO TOP))
		     [, (COND
			  ((AND (NULL OPEN)
				(NEQ ANYINPUT? (QUOTE OP)))
			    (SETQ FROM U)
			    (SETQ U 0)
			    (SETQ ANYINPUT? NIL))
			  (T 

          (* The, is part of an instruction.
          Setting FROM to T indicates that a, has been seen in 
          the instruction. Before FROM is T, any number or 
          variable typed in is shifted to AC field, unless 
          already there.)


			     (SETQ FROM T]
		     (:(SETQ SYMLST
			 (CONS (SETQ EDITAX
				 (CONS (EDITADR (COND
						  (OPEN Q)
						  ((ZEROP U)
						    %.)
						  (T U)))
				       (EDITAREED)))
			       SYMLST))
		       (SETQ USERSYMS (CONS (CONS (CDR EDITAX)
						  (CAR EDITAX))
					    USERSYMS)))
		     (; (COND
			  ((OR (NULL OPEN)
			       (ILESSP %. BOXED))
			    (ERROR!)))
			(SETQ LEFTFLAG T)

          (* Indicates that user has typed in a left half.
          A flag is needed because the left half he typed in 
          might have been NIL.)


			(SETQ LEFTHALF U)
			(SETQ U 0)
			(SETQ ANYINPUT? NIL))
		     (W (EDITAW))
		     (C                       (* Same as w except 
                                                counts occurrences, only
                                                printing first and 
                                                last.)
			  (EDITAW T))
		     (%
 (COND
			    ((NOT (ILESSP %. $))
			      (PRINT (QUOTE (AT END OF BLOCK))
				     T T)
			      (ERROR!)))
			  (EDITA2 (ADD1 %.)
				  T)
			  (GO TOP))
		     (PROGN (COND
			      ((NULL ANYINPUT?)
				(SETQ ANYINPUT? T)))
			    (SETQ U (EDITA3 EDITAX U))
			    (COND
			      ((OR (NUMBERP U)
				   (NLISTP EDITAX)))
			      (OPEN (PRIN1 (QUOTE % )
					   T))
			      (T (TERPRI T)
				 (SETQ EDITAX (QUOTE %
))
				 (GO TOP])

(EDITASYM
  [LAMBDA (N)

          (* Looks up N on SYMLST but returns its name only if 
          also on USERSYMS. Used for decoding address to left 
          of / only, i.e. shouldn't DECODE 15 to PP, 8 to FX, 
          etc.)


    (PROG (X)
          (RETURN (COND
                    ([NULL (SETQ X (CDR (SASSOC N SYMLST]
                      N)
                    ((FASSOC X USERSYMS)
                      X)
                    (T N])

(EDITAREED
  [LAMBDA NIL
    (PROG (X)
          (COND
	    ((LISTP (SETQ X (READ T EDITARDTBL T)))
                                                (* Extra argument 
                                                suppresses carriage 
                                                return.)
	      (SPACES 1 T)))
          (RETURN X])

(EDITAPRINT
  [LAMBDA (X FILE)
    (COND
      ((NULL FILE)
	(SETQ FILE T)))
    (COND
      (LEFTHALF (PRIN2 (SETQ Q LEFTHALF)
		       FILE T)
		(PRIN1 (QUOTE " ; ")
		       FILE)
		(PRIN2 (SETQ Q X)
		       FILE T))
      (T (PRIN2 (SETQ Q X)
		FILE T])

(EDITAW
  [LAMBDA (QUIETFLG)                            (* E (RADIX 777777777770Q
))
    (RESETVAR EDITQUIETFLG T
      (PROG ((CNT 0)
	     MSK N1 N2 X W TEM)
	    [COND
	      ((NULL ANYINPUT?)
		(SETQ U (EDITFPAT (READ T T]
	    [SETQ N1 (COND
		((NUMBERP FROM) FROM)
		(ANYINPUT? 0)
		(T (IPLUS BOXED]
	    (COND
	      ((AND (ILESSP N1 BOXED)
		    (NOT (NUMBERP U)))        (* Can only scan 
                                                instructions for a 
                                                number.)
		(SETQ N1 BOXED)))
	    (COND
	      ((ILESSP N1 BOXED)
		(SETQ N2 BOXED)
		[COND
		  (MASK (SETQ MSK MASK))
		  ((OR (ZEROP (SETQ X (LOGAND U 777777000000Q)))
		       (EQP X 777777000000Q))   (* All zeroes or all 
                                                ones in left half, 
                                                therefore just an 
                                                address.)
		    (SETQ MSK 777777Q))
		  (T (SETQ MSK 0)

          (* For each field (address, AC, indirect, index, or 
          instruction) that contains any ones, include that 
          field in the mask.)


		     (MAPC (QUOTE (777777Q 17000000Q 20000000Q 
					   740000000Q 777000000000Q))
			   (FUNCTION (LAMBDA (X)
			       (COND
				 ((NOT (ZEROP (LOGAND U X)))
				   (SETQ MSK (LOGOR MSK X]
		(SETQ X (LOGAND U MSK)))
	      (T (SETQ N2 (ADD1 $))
		 (SETQ X U)))
	LP  (SETQ W (EDITAOPENR N1 W))
	    (COND
	      ([NULL (COND
		       (MSK (EQP (SETN TEM (LOGAND W MSK))
				 X))
		       (T (OR (EDIT4E X W)
			      (AND LEFTHALF (EDIT4E X LEFTHALF]
		(GO LP1))
	      ((AND (NEQ (SETQ CNT (ADD1 CNT))
			 1)
		    QUIETFLG)
		(SETQ %. (IPLUS N1))
		(GO LP1)))
	    (PRIN2 (EDITASYM (SETQ %. (IPLUS N1)))
		   T T)
	    (PRIN2 (QUOTE /)
		   T T)
	    (SPACES 3 T)
	    (COND
	      (MSK (DECODE (IPLUS W)
			   T))
	      (T (EDITAPRINT W)))
	    (TERPRI T)
	LP1 (COND
	      ((ILESSP (SETN N1 (ADD1 N1))
		       N2)
		(GO LP))
	      ((AND (NULL QUIETFLG)
		    (ILESSP CNT 5))
		(TERPRI T))
	      (T (PRIN1 CNT T)
		 (PRIN1 (QUOTE " OCCURRENCES.

")
			T)))
	    (SETQ FROM NIL)
	    (SETQ ANYINPUT? NIL)
	    (SETQ U 0)                        (* E (RADIX 10))
	    (RETURN %.])

(EDITA2
  [LAMBDA (X FLG)                               (* Handles linefeeds, 
                                                tabs, and.)
    (COND
      [(NULL (NUMBERP X))
	(ERROR (CONS X (QUOTE (NOT ARRAY NUMBER]
      ((AND (NULL DEF)
	    (IGREATERP X $))
	(ERROR!)))
    (COND
      ((OR (NULL FLG)
	   (IGREATERP (POSITION T)
		      58))
	(TERPRI T))
      (T (PRIN1 (CHARACTER 13)
		T)
	 (PRIN1 (CHARACTER 13)
		T)))
    (EDITACLOSER U)
    (PRIN2 (EDITASYM (SETQ U X))
	   T T)
    (PRIN2 (SETQ EDITAX (QUOTE /))
	   T T)                                 (* EDITA1 will now open 
                                                register and print 
                                                indicated quantity.)
    (SETQ OPEN NIL])

(EDITA3
  [LAMBDA (EDITAX EDITAY)                       (* E (RADIX 777777777770Q
))                                              (* Handles inputs.)
    (PROG (EDITAZ (@ 20000000Q))
          [RETURN (COND
		    ((NULL DEF)
		      (GO NOTFN))
		    [(AND OPEN (NOT (ILESSP %. BOXED)))
		      (COND
			((ATOM EDITAX)
			  EDITAX)
			(T (EVALA EDITAX USERSYMS]
		    ([NUMBERP (SETQ EDITAZ (GETP EDITAX (QUOTE OPD]
		      (SETQ ANYINPUT? (QUOTE OP))
		      (IPLUS EDITAY (LLSH EDITAZ 22Q)))
		    [(EQ ANYINPUT? (QUOTE OP))
		      (COND
			((NOT (ATOM EDITAX))
			  [COND
			    ((ILESSP (SETQ EDITAX (EDITA4 (CAR EDITAX)))
				     1000000Q)
                                                (* Index register.)
			      (SETQ EDITAX (LSH EDITAX 22Q]
			  (IPLUS EDITAY EDITAX))
			((AND (IGREATERP (SETQ EDITAX (EDITA4 EDITAX))
					 0)
			      (NULL FROM))
			  [COND
			    ((AND (ILESSP EDITAX 20Q)
				  (ZEROP (LOGAND EDITAY 777777Q)))
                                                (* AC.)
			      (SETQ EDITAX (LSH EDITAX 27Q]
			  (IPLUS EDITAY EDITAX))
			((ILESSP (ABS EDITAX)
				 1000000Q)      (* Address arithmetic.)
			  (LOGOR (LOGAND EDITAY 777777000000Q)
				 (LOGAND (IPLUS EDITAY EDITAX)
					 777777Q)))
			(T (IPLUS EDITAY EDITAX]
		    ((EQ EDITAX (QUOTE &))

          (* Used to set up a search involving AC, index, 
          and/or indirect field but no special instruction.)


		      (SETQ ANYINPUT? (QUOTE OP))
		      EDITAY)
		    ([NOT (NUMBERP (SETQ EDITAX (EDITA4 EDITAX]
		      EDITAX)
		    (T (IPLUS EDITAY EDITAX]    (* E (RADIX 10))
      NOTFN
          (RETURN (COND
		    [OPEN (COND
			    ((EQ EDITAX (QUOTE Q))
                                                (* Always evaluate q.)
			      Q)
			    ((ATOM EDITAX)
			      EDITAX)
			    (T (EVALA EDITAX USERSYMS]
		    ((NUMBERP (SETQ EDITAX (EVALA EDITAX USERSYMS)))
		      (IPLUS EDITAY EDITAX))
		    (T EDITAX])

(EDITA4
  [LAMBDA (EDITAX)
    (PROG (EDITAY)
          (RETURN (COND
                    ([AND (LITATOM EDITAX)
                          (SETQ EDITAY (GETP EDITAX (QUOTE COREVAL]
                      EDITAY)
                    (T (EVALA EDITAX USERSYMS])

(EDITALIT
  [LAMBDA (X)                                   (* Searches for literal 
                                                X, returns its address.)
    (RESETVAR EDITQUIETFLG T (PROG ([N1 (IPLUS LITS (COND
						 (DEF 0)
						 (T -1]
				    (N2 $))
			           (SETQ X (EDITFPAT X))
			       LP  [COND
				     ([IGREATERP N1
						 (COND
						   (DEF N2)
						   (T (IPLUS N2 -1]
				       (RETURN NIL))
				     ([COND
					 ((ATOM X)
					   (EQ (FNOPENRA ORG N1)
					       X))
					 (T (EDIT4E X (FNOPENRA ORG N1]
				       (RETURN (IPLUS N1
						      (COND
							(DEF 0)
							(T 1]
			           (SETN N1 (ADD1 N1))
			           (GO LP])

(DECODE
  [LAMBDA (X FILE)
    (SETQ Q X)                                (* E (RADIX 777777777770Q
))
    (PROG (Y Z INDEX)
          (SETN LEFTX (LRSH X 22Q))
          (SETN RIGHTX (LOGAND X 777777Q))
          (COND
	    ([OR (NULL DEF)
		 (ZEROP (SETQ Y (RSH LEFTX 11Q)))
		 (IGREATERP Y 700Q)
		 (NULL (SETQ Y (ELT OPCODEARRAY Y]
	      (PRIN2 X FILE T)
	      (RETURN)))
          (PRIN1 Y FILE)
          (SPACES 1 FILE)
          (COND
	    ([NOT (ZEROP (SETQ Y (LOGAND LEFTX 740Q]
                                                (* Accumulator.)
	      (PRIN1 (SELECTQ (SETQ Y (RSH Y 5))
			      (14Q (QUOTE BR))
			      (15Q (QUOTE NP))
			      (16Q (QUOTE PP))
			      (17Q (QUOTE CP))
			      Y)
		     FILE)
	      (PRIN1 (QUOTE ,)
		     FILE)))
          (COND
	    ((NOT (ZEROP (LOGAND LEFTX 20Q)))
	      (PRIN1 (QUOTE @)
		     FILE)))
          (SETQ INDEX (LOGAND LEFTX 17Q))
          (COND
	    ((OR (SETQ Y (SASSOC RIGHTX SYMLST))
		 (SETQ Y (SASSOC [SETQ Z (IDIFFERENCE
				     RIGHTX
				     (COND
				       ((HANDLEP ORG)
					 0)
				       (T (LOC ORG]
				 SYMLST)))      (* Defined symbol.)
	      (PRIN1 (CDR Y)
		     FILE))
	    ((BETWEEN Z BOXED $)                (* Literal)
	      (PRIN1 (QUOTE ')
		     FILE)
	      (PRIN2 (FNOPENRA ORG Z)
		     FILE T))
	    ((BETWEEN Z 0 (SUB1 BOXED))
	      (PRIN1 Z FILE))
	    ((IGREATERP RIGHTX 777000Q)
	      (PRIN1 (IDIFFERENCE RIGHTX 1000000Q)
		     FILE))
	    (T (PRIN1 RIGHTX FILE)))
          (COND
	    ((NOT (ZEROP INDEX))
	      (PRIN1 (LIST (SELECTQ INDEX
				    (14Q (QUOTE BR))
				    (15Q (QUOTE NP))
				    (16Q (QUOTE PP))
				    (17Q (QUOTE CP))
				    INDEX))
		     FILE)))                    (* E (RADIX 10))
          (RETURN])

(EDITADR
  [LAMBDA (X)
    (COND
      ((BETWEEN X (COND
		  (DEF -2)
		  (T -1))
		$)
	X)
      (T (PROG (Y)
	       (SETQ X (LOGAND X 262143))
	       [SETQ Y (IDIFFERENCE X (COND
				      ((HANDLEP ORG)
					0)
				      (DEF (LOC ORG))
				      (T (ADD1 (LOC ORG]
	       (RETURN (COND
			 ((BETWEEN Y (COND
				     (DEF -2)
				     (T -1))
				   $)
			   Y)
			 (T X])

(EDITAOPENR
  [LAMBDA (N M)

          (* Sort of a fancy ELT. If.
          Is in unboxed region, does an OPENR, which returns a 
          full word as a number. If in boxed region, does a 
          CAR of VAG, which is equivalent to ELT.)


    (SETQ LEFTHALF NIL)
    (SETQ LEFTFLAG NIL)
    (COND
      ((NOT (BETWEEN %. (COND
		       (DEF -2)
		       (T -1))
		     $))
	(OPENR N))
      [(NOT (ILESSP N BOXED))
	(AND (NEQ [SETQ M (FNOPENRD ORG (IPLUS N (COND
						 (DEF 0)
						 (T -1]
		  (VAG 0))
	     (SETQ LEFTHALF M))
	(FNOPENRA ORG (IPLUS N (COND
			       (DEF 0)
			       (T -1]
      (M                                        (* Use M for boxing the 
                                                number.)
	 [SETN M (FNOPENR ORG (IPLUS N (COND
				       (DEF 0)
				       (T -1]
	 (AND DEF (HANDLEP ORG)
	      (EQ (LOGAND (LRSH M 18)
			  15)
		  12)
	      (SETN M (IDIFFERENCE M 2)))
	 M)
      (T [SETQ N (FNOPENR ORG (IPLUS N (COND
				       (DEF 0)
				       (T -1]
	 (AND DEF (HANDLEP ORG)
	      (EQ (LOGAND (LRSH N 18)
			  15)
		  12)
	      (SETN N (IDIFFERENCE N 2)))
	 N])

(EDITACLOSER
  [LAMBDA (X)
    (COND
      (OPEN (SETQ OPEN NIL)
	    [COND
	      ((AND (NULL ANYINPUT?)
		    (NULL LEFTFLAG)))
	      ((NULL (BETWEEN %. (COND
				(DEF -2)
				(T -1))
			      $))               (* User can look outside
                                                of array, but cannot 
                                                change.)
		(ERROR!))
	      [(ILESSP %. BOXED)
		(FNCLOSER ORG (COND
			    (DEF %.)
			    (T (SUB1 %.)))
			  (COND
			    ((AND DEF (HANDLEP ORG)
				  (EQ (LOGAND (LRSH X 18)
					      15)
				      12))
			      (IPLUS X 2))
			    (T X]
	      (T (AND ANYINPUT? (FNCLOSERA ORG (COND
					     (DEF %.)
					     (T (SUB1 %.)))
					   X))
		 (AND LEFTFLAG (FNCLOSERD ORG (COND
					    (DEF %.)
					    (T (SUB1 %.)))
					  LEFTHALF]
	    T])

(BETWEEN
  [LAMBDA (X Y Z)                               (* X between Y and Z 
                                                inclusive.)
    (NOT (OR (ILESSP X Y)
             (IGREATERP X Z])
)
  (RPAQ SYMLST)
  (RPAQ USERSYMS)
[DECLARE: DOEVAL@COMPILE DONTCOPY 
(DEFLIST(QUOTE(
  [BETWEEN (LAMBDA (X Y Z)
		   (NOT (OR (ILESSP X Y)
			    (IGREATERP X Z]
))(QUOTE BLKLIBRARYDEF))
]
  [RPAQQ EDITABLOCKS
	 ((EDITA EDITA EDITA1 EDITASYM EDITAREED EDITAPRINT EDITAW 
		 EDITA2 EDITA3 EDITA4 EDITALIT DECODE EDITADR 
		 EDITAOPENR EDITACLOSER (ENTRIES EDITA)
		 (BLKLIBRARY BETWEEN)
		 (LOCALFREEVARS EDITARRY DEF ANYINPUT? Q U OPEN 
				FROM MASK %. BOXED LITS ORG $ #VARS 
				LEFTX RIGHTX FILE LEFTFLAG LEFTHALF 
				EDITAX)
		 (SPECVARS DEF ANYINPUT? Q U FROM %. BOXED LITS ORG 
			   $ FILE @)
		 (GLOBALVARS EDITARDTBL USERSYMS SYMLST OPCODEARRAY]
[DECLARE: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY
  (BLOCK: EDITA EDITA EDITA1 EDITASYM EDITAREED EDITAPRINT EDITAW 
	  EDITA2 EDITA3 EDITA4 EDITALIT DECODE EDITADR EDITAOPENR 
	  EDITACLOSER (ENTRIES EDITA)
	  (BLKLIBRARY BETWEEN)
	  (LOCALFREEVARS EDITARRY DEF ANYINPUT? Q U OPEN FROM MASK 
			 %. BOXED LITS ORG $ #VARS LEFTX RIGHTX FILE 
			 LEFTFLAG LEFTHALF EDITAX)
	  (SPECVARS DEF ANYINPUT? Q U FROM %. BOXED LITS ORG $ FILE 
		    @)
	  (GLOBALVARS EDITARDTBL USERSYMS SYMLST OPCODEARRAY))
]
  (AND (NULL (GETD (QUOTE LOADUP)))
       (EDITAINIT))
(DECLARE: DONTCOPY
  (FILEMAP (NIL (603 20898 (EDITAINIT 615 . 1601) (EDITA 1605 . 4833)
(EDITA1 4837 . 9326) (EDITASYM 9330 . 9791) (EDITAREED 9795 . 10140)
(EDITAPRINT 10144 . 10418) (EDITAW 10422 . 12737) (EDITA2 12741
. 13515) (EDITA3 13519 . 15507) (EDITA4 15511 . 15776) (EDITALIT 15780
. 16471) (DECODE 16475 . 18282) (EDITADR 18286 . 18685) (EDITAOPENR
18689 . 19848) (EDITACLOSER 19852 . 20693) (BETWEEN 20697 . 20895)))))
STOP
