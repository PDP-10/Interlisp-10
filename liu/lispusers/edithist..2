(FILECREATED "19-DEC-81 15:27:44" EDITHIST.;5   13797

     changes to:  EDITHISTCOMS

     previous date: "19-DEC-81 15:10:10" EDITHIST.;4)


(PRETTYCOMPRINT EDITHISTCOMS)

(RPAQQ EDITHISTCOMS [(FILEPKGCOMS EDITHIST)
		     (ADDVARS (MAKEFILEFORMS (INITEDITHIST FILE)))
		     (FNS CHECKEDITHIST DELEDITHISTDEF GETEDITHISTDEF INITEDITHIST MAKEDITHIST 
			  MERGEEDITHIST PUTEDITHISTDEF SHRINKEDITHIST)
		     (* EDITHISTALIST is the home of all edit histories. ASKEDITHIST controls the 
			activities of the EDITHISTORY package during MAKEFILE. If ASKEDITHIST=NIL, 
			nothing is ever added to any history list. If ASKEDITHIST=NOCOMMENT, ASKUSER 
			is not called and no session coments ARE recorded, but the DATE, user and 
			CHANGE list ARE added to the history list. If ASKEDITHIST=COMMENT, ASKUSER 
			will be called to ger a comment line from the user, which will be 
			incorporated into the edit history. Finally, if ASKEDITHIST=T, the edit 
			history list will create a new edit history for any FILE that is written that 
			DOESN'T HAVE one, and will otherwise behave as ASKEDITHIST=COMMENT. Finally, 
			if option "ASK" is given to makefile, Comments will be asked for, whatever 
			the setting of ASKEDITHIST)
		     (ALISTS (EDITHISTALIST))
		     [VARS (ASKEDITHIST T)
			   (LIMITEDITHIST (QUOTE (10 . 30]
		     (GLOBALVARS ASKEDITHIST LIMITEDITHIST)
		     (* Edit histories ARE all maintained on the ALIST EDITHISTALIST. The FORM of the 
			individual ALIST entry is (histname (DATE USERID CHANGELIST COMMENTSLIST)
							    (DATE USERID CHANGELIST COMMENTSLIST)
							    ...)
			when there ARE several edithistories on a single EDITHIST command, the first 
			is regarded as the currently active one, the others ARE treated as archival 
			and ARE not added to)
		     (EDITHIST * EDITHISTEDITHIST)
		     (DECLARE: DONTCOPY (RECORDS * EDITHISTRECORDS))
		     (DECLARE: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS
			       (ADDVARS (NLAMA MAKEDITHIST)
					(NLAML)
					(LAMA])
(FILEPKGCOM (QUOTE EDITHIST)
	    (QUOTE MACRO)
	    [QUOTE (X (COMS * (MAKEDITHIST . X]
	    (QUOTE CONTENTS)
	    (QUOTE CHECKEDITHIST))
(FILEPKGTYPE (QUOTE EDITHIST)
	     (QUOTE DESCRIPTION)
	     (QUOTE "file edit history")
	     (QUOTE GETDEF)
	     (QUOTE GETEDITHISTDEF)
	     (QUOTE PUTDEF)
	     (QUOTE PUTEDITHISTDEF)
	     (QUOTE DELDEF)
	     (QUOTE DELEDITHISTDEF))

(ADDTOVAR MAKEFILEFORMS (INITEDITHIST FILE))
(DEFINEQ

(CHECKEDITHIST
  [LAMBDA (COM NAME TYPE)                       (* DD: " 8-Jul-80 18:06"
)

          (* Three classes of FILEPKGTYPE ARE recognised.
	  For the FORM (foo * bar) bar is recognised as VARS.
	  For the FORM (foo * bar) the contents of bar ARE 
	  recognised as a foo. For the FORM 
	  (foo bar1 bar2 ...) the bars ARE recognised as foos)


    (PROG [(NAMEIT (OR (AND (EQ TYPE (QUOTE VARS))
			    (EQ (CADR COM)
				(QUOTE *))
			    (CDDR COM))
		       (AND (EQ TYPE (CAR COM))
			    (COND
			      ((EQ (CADR COM)
				   (QUOTE *))
				(GETATOMVAL (CADDR COM)))
			      (T (CDR COM]

          (* NAMEIT is the recognised list.
	  Now MATCH the list NAME against in the canonical way
	  and return the result)


          (RETURN (COND
		    ((EQ NAME T)
		      (NEQ NAMEIT NIL))
		    ((LISTP NAME)
		      (INTERSECTION NAME NAMEIT))
		    ((NULL NAME)
		      NAMEIT)
		    ((FMEMB NAME NAMEIT)
		      T])

(DELEDITHISTDEF
  [LAMBDA (NAME TYPE)                                       (* DD: " 5-Jan-81 23:57")
    (/PUTASSOC NAME NIL EDITHISTALIST)
    (MARKASCHANGED NAME TYPE])

(GETEDITHISTDEF
  [LAMBDA (NAME TYPE OPTIONS)                               (* DD: " 5-Jan-81 23:55")
    (CDR (ASSOC NAME EDITHISTALIST])

(INITEDITHIST
  [LAMBDA (FILE)                                            (* DD: "19-DEC-81 15:09")
    (PROG ((FILE (U-CASE FILE))
	   (ASK (QUOTE (ASK ask COMMENT comment)))
	   (NOASK (QUOTE (NOASK noask NOCOMMENT nocomment)))
	   FD OLDP)
          (COND
	    ((INTERSECTION ASK OPTIONS)
	      (SETQ OPTIONS (LDIFFERENCE OPTIONS ASK))
	      (RESETSAVE ASKEDITHIST T)))
          [COND
	    ((INTERSECTION NOASK OPTIONS)
	      (SETQ OPTIONS (LDIFFERENCE OPTIONS NOASK))
	      (RESETSAVE ASKEDITHIST (QUOTE NOCOMMENT]
          (SETQ OLDP (INFILECOMS? T (QUOTE EDITHIST)
				  (FILECOMS FILE)))
          (COND
	    (OLDP (AND (NULL (FASSOC FILE EDITHISTALIST))
		       [SETQ FD (CDAR (GETPROP FILE (QUOTE FILEDATES]
		       (LOADVARS (QUOTE EDITHISTALIST)
				 FD)))
	    ((OR (EQ ASKEDITHIST T))
	      (ADDTOFILE FILE (QUOTE EDITHIST)
			 FILE])

(MAKEDITHIST
  [NLAMBDA A                                                (* DD: " 5-Jan-81 12:53")
                                                            (* Makeedithist is invoke by the contents property of 
							    the EDITHIST filepkgtype. See the manual for details of 
							    its arguments and functional specification)
    [AND
      CHANGES ASKEDITHIST
      (RESETFORM
	(RAISE NIL)
	(PROG [(NAME (U-CASE (CAR A]
	      (PUTASSOC
		NAME
		[SHRINKEDITHIST												     |
		  (NCONC (LISTP (CDR (FASSOC NAME EDITHISTALIST)))												     |
			 (LIST (NCONC (LIST (DATE)												     |
					    (OR INITIALS USERNAME)												     |
					    (OUTFILE)												     |
					    CHANGES)												     |
				      (AND (NEQ ASKEDITHIST (QUOTE NOCOMMENT))												     |
					   (ASKUSER 10 (QUOTE %])												     |
						    "comments : "												     |
						    (QUOTE ((%] " none
")												     |
							     ))												     |
						    NIL NIL (LIST (QUOTE RETURN)												     |
								  (QUOTE (COND												     |
									   ((EQUAL ANSWER												     |
										   (QUOTE (%])))												     |
									     NIL)												     |
									   (ANSWER]												     |
		(OR (LISTP EDITHISTALIST)
		    (SETQ EDITHISTALIST (LIST (LIST NAME]
    (LIST (LIST (QUOTE DECLARE:)
		(QUOTE DONTCOPY)
		(LIST (QUOTE ALISTS)
		      (CONS (QUOTE EDITHISTALIST)
			    A])

(MERGEEDITHIST
  [LAMBDA (NEWER OLDER)                                     (* DD: " 5-Jan-81 16:29")												     |
    (PROG ((NEW (CAR NEWER))												     |
	   (OLD (CAR OLDER))												     |
	   TEMP)												     |
          [replace (EHREC DATE) of NEW with (CONS (COND												     |
						    ((NLISTP (SETQ TEMP (fetch (EHREC DATE)												     |
									   of NEW)))												     |
						      TEMP)												     |
						    (T (CAR TEMP)))												     |
						  (COND												     |
						    ((NLISTP (SETQ TEMP (fetch (EHREC DATE)												     |
									   of OLD)))												     |
						      TEMP)												     |
						    (T (CDR TEMP]												     |
          [COND												     |
	    ((NOT (EQUAL (fetch (EHREC EDITOR) of NEW)												     |
			 (fetch (EHREC EDITOR) of OLD)))												     |
	      (replace (EHREC EDITOR) of NEW with (UNION (MKLIST (fetch (EHREC EDITOR) of NEW))												     |
							 (MKLIST (fetch (EHREC EDITOR) of OLD]												     |
          [replace (EHREC FILE) of NEW with (CONS (COND												     |
						    ((NLISTP (SETQ TEMP (fetch (EHREC FILE)												     |
									   of NEW)))												     |
						      TEMP)												     |
						    (T (CAR TEMP)))												     |
						  (COND												     |
						    ((NLISTP (SETQ TEMP (fetch (EHREC FILE)												     |
									   of OLD)))												     |
						      TEMP)												     |
						    (T (CDR TEMP]												     |
          [replace (EHREC CHANGES) of NEW with (UNION (MKLIST (fetch (EHREC CHANGES) of NEW))												     |
						      (MKLIST (fetch (EHREC CHANGES) of OLD]												     |
          (replace (EHREC COMMENTS) of NEW with (APPEND (fetch (EHREC COMMENTS) of NEW)												     |
							(fetch (EHREC COMMENTS) of OLD)))												     |
          (RPLACD NEWER (CDR OLDER])

(PUTEDITHISTDEF
  [LAMBDA (NAME TYPE DEFINITION)                            (* DD: " 5-Jan-81 23:57")
    (/PUTASSOC NAME DEFINITION EDITHISTALIST)
    (MARKASCHANGED NAME TYPE])

(SHRINKEDITHIST
  [LAMBDA (EH)                                              (* DD: " 5-Jan-81 17:15")												     |
    (PROG ((SOFTLIMIT (CAR LIMITEDITHIST))												     |
	   (HARDLIMIT (CDR LIMITEDITHIST)))												     |
          (RETURN (COND												     |
		    [(AND (FIXP SOFTLIMIT)												     |
			  (FIXP HARDLIMIT)												     |
			  (IGREATERP (FLENGTH EH)												     |
				     HARDLIMIT))												     |
		      (PROG ((OL (REVERSE EH))												     |
			     WL)												     |
			    [for PASS from 1 to 2												     |
			       do (SETQ WL (NTH OL SOFTLIMIT))												     |
				  [for WZ on (CDR WL) as I from (ADD1 SOFTLIMIT) when (CAR WL)												     |
				     do (COND												     |
					  ([AND (NULL (fetch (EHREC COMMENTS) of (CAR WZ)))												     |
						(OR (EQ PASS 2)												     |
						    (INTERSECTION (MKLIST (fetch (EHREC EDITOR)												     |
									     of (CAR WZ)))												     |
								  (MKLIST (fetch (EHREC EDITOR)												     |
									     of (CAR WL]                                                                                                     |
                                                            (* merge if the mergee has no comments and has the same 												     |
							    author)												     |
					    (MERGEEDITHIST WL WZ))												     |
					  ((AND (EQ PASS 2)												     |
						(IGREATERP I HARDLIMIT))												     |
					    (MERGEEDITHIST WL WZ))												     |
					  (T (SETQ WL WZ]												     |
				  (AND (ILEQ (FLENGTH OL)												     |
					     HARDLIMIT)												     |
				       (SETQ PASS (ADD1 PASS]												     |
			    (PRINTOUT T " << edit history reduced to " (FLENGTH OL)												     |
				      " entries >>" T)												     |
			    (RETURN (DREVERSE OL]												     |
		    (T EH])
)



(* EDITHISTALIST is the home of all edit histories. ASKEDITHIST controls the activities of the 
EDITHISTORY package during MAKEFILE. If ASKEDITHIST=NIL, nothing is ever added to any history list. If
 ASKEDITHIST=NOCOMMENT, ASKUSER is not called and no session coments ARE recorded, but the DATE, user 
and CHANGE list ARE added to the history list. If ASKEDITHIST=COMMENT, ASKUSER will be called to ger a
 comment line from the user, which will be incorporated into the edit history. Finally, if 
ASKEDITHIST=T, the edit history list will create a new edit history for any FILE that is written that 
DOESN'T HAVE one, and will otherwise behave as ASKEDITHIST=COMMENT. Finally, if option "ASK" is given 
to makefile, Comments will be asked for, whatever the setting of ASKEDITHIST)


(ADDTOVAR EDITHISTALIST )

(RPAQQ ASKEDITHIST T)

(RPAQQ LIMITEDITHIST (10 . 30))
(DECLARE: DOEVAL@COMPILE DONTCOPY

(ADDTOVAR GLOBALVARS ASKEDITHIST LIMITEDITHIST)
)



(* Edit histories ARE all maintained on the ALIST EDITHISTALIST. The FORM of the individual ALIST 
entry is (histname (DATE USERID CHANGELIST COMMENTSLIST) (DATE USERID CHANGELIST COMMENTSLIST) ...) 
when there ARE several edithistories on a single EDITHIST command, the first is regarded as the 
currently active one, the others ARE treated as archival and ARE not added to)


(RPAQQ EDITHISTEDITHIST (EDITHISTEDITHIST))
(DECLARE: DONTCOPY 

(ADDTOVAR EDITHISTALIST (EDITHISTEDITHIST (" 5-Jan-81 16:04:47" DD: <DDYER>EDITHIST.LISP.38
								(SHRINKEDITHIST MERGEEDITHIST EHREC))
					  (" 5-Jan-81 17:10:02" DD: <DDYER>EDITHIST.LISP.39
								(LIMITEDITHIST MERGEEDITHIST 
									       SHRINKEDITHIST)
								(completed shrinking functions))
					  (" 5-Jan-81 17:15:29" DD: <DDYER>EDITHIST.LISP.41
								(LIMITEDITHIST SHRINKEDITHIST)
								(added a message))
					  (" 5-Jan-81 23:24:57" DD: <DDYER>EDITHIST.LISP.42
								(EDITHISTCOMS INITEDITHIST)
								(adding logic to load edithist when 
									only the compiled file has 
									been loaded so far))
					  (" 5-Jan-81 23:43:09" DD: <DDYER>EDITHIST.LISP.43 (
								  INITEDITHIST)
								(adding code to load edit history if 
									only the compiled file has 
									been loaded so far))
					  (" 5-Jan-81 23:58:38" DD: <DDYER>EDITHIST.LISP.44
								(EDITHIST DELEDITHISTDEF 
									  PUTEDITHISTDEF 
									  GETEDITHISTDEF))
					  ("30-Aug-81 23:09:26" DD: <DDYER>EDITHIST.LISP.45 (
								  EDITHISTCOMS))
					  ("19-DEC-81 14:50:15" DD: EDITHIST.;2 (EDITHISTCOMS 
										     INITEDITHIST))
					  ("19-DEC-81 15:00:26" DD: EDITHIST.;3 (ASKEDITHIST 
										     INITEDITHIST)
								(added "ASK" option to makefile))
					  ("19-DEC-81 15:10:25" DD: EDITHIST.;4 (EDITHISTCOMS 
										     INITEDITHIST))
					  ("19-DEC-81 15:27:56" DD: EDITHIST.;5 (EDITHISTCOMS))))
)
(DECLARE: DONTCOPY 

(RPAQQ EDITHISTRECORDS (EHREC))
[DECLARE: EVAL@COMPILE 

(RECORD EHREC (DATE EDITOR FILE CHANGES . COMMENTS))
]
)
(DECLARE: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS 

(ADDTOVAR NLAMA MAKEDITHIST)

(ADDTOVAR NLAML )

(ADDTOVAR LAMA )
)
(DECLARE: DONTCOPY
  (FILEMAP(NIL (2506 10541 (CHECKEDITHIST 2518 . 3482) (DELEDITHISTDEF 3486 . 3666) (GETEDITHISTDEF 3670 . 3816) (INITEDITHIST 3820 . 4707) (MAKEDITHIST 4711 . 6205) (MERGEEDITHIST 6209 . 8310) (PUTEDITHISTDEF 8314 . 8501) (SHRINKEDITHIST 8505 . 10538)))))
STOP
TOP
