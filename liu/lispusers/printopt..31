(FILECREATED " 4-Feb-83 14:59:41" <DDYER>PRINTOPT..31 15105  

      previous date: " 3-FEB-83 10:22:04" <DDYER>PRINTOPT..30)


(PRETTYCOMPRINT PRINTOPTCOMS)

(RPAQQ PRINTOPTCOMS [(FNS * PRINTOPTFNS)
		     (EDITHIST * PRINTOPTEDITHIST)
		     (BLOCKS * PRINTOPTBLOCKS)
		     (IFPROP HELP GETOPTIMUM PRINTOPTIMUM)
		     (P (RELINK (QUOTE LISPX])

(RPAQQ PRINTOPTFNS (GETOPTIMUM PNCHARS PNCHARS1 PNCHARS2 PPOBJ PRINTCAUTIOUSLY PRINTOPTIMUM 
			       SELPRINTLEVEL SHOWPRIN2 SHOWPRINT))
(DEFINEQ

(GETOPTIMUM
  [LAMBDA (OBJECT SPACE USEPRIN2)                           (* DD: "30-OCT-82 11:25")
    (OR SPACE (SETQ SPACE (LINELENGTH)))
    (PNCHARS OBJECT (CAR (SETQ SPACE (SELPRINTLEVEL OBJECT (IQUOTIENT (ITIMES SPACE 3)
								      4)
						    SPACE USEPRIN2)))
	     (CDR SPACE)
	     T USEPRIN2])

(PNCHARS
  [LAMBDA (OBJECT CARN CDRN RETOBJECT USEPRIN2 LIMIT)       (* DD: " 7-MAY-82 00:52")
    (PROG ((SUMCAR (PRINTLEVEL)))
          (OR (SMALLP LIMIT)
	      (SETQ LIMIT 1000))
          (OR CARN (SETQ CARN (CAR SUMCAR)))
          (OR CDRN (SETQ CDRN (CDR SUMCAR)))
          (SETQ SUMCAR (SUB1 (IDIFFERENCE CDRN CARN)))
          (RETURN (APPLY* (COND
			    (RETOBJECT (FUNCTION PNCHARS2))
			    (T (FUNCTION PNCHARS1)))
			  OBJECT CARN CDRN NIL LIMIT])

(PNCHARS1
  [LAMBDA (OBJECT CARN CDRN TAILP LIMIT)                    (* DD: " 7-MAY-82 01:09")
    (PROG (VAL)
          (DECLARE (LOCALVARS VAL))
          (RETURN (COND
		    [(IGREATERP LIMIT 0)
		      (COND
			[(NLISTP OBJECT)
			  (COND
			    (TAILP (IPLUS 4 (NCHARS OBJECT USEPRIN2)))
			    (T (NCHARS OBJECT USEPRIN2]
			((ZEROP CARN)
			  1)
			((ZEROP CDRN)
			  (COND
			    (TAILP 4)
			    (1)))
			((IPLUS (SETQ VAL (ADD1 (PNCHARS1 (CAR OBJECT)
							  (SUB1 CARN)
							  (IPLUS SUMCAR CARN)
							  NIL LIMIT)))
				(COND
				  ((CDR OBJECT)
				    (PNCHARS1 (CDR OBJECT)
					      CARN
					      (SUB1 CDRN)
					      T
					      (IDIFFERENCE LIMIT VAL)))
				  (1]
		    (T 10])

(PNCHARS2
  [LAMBDA (OBJECT CARN CDRN TAILP)                          (* DD: "29-Sep-80 14:23")
                                                            (* This function produces an actual sexpr that will 
							    PRIN1 as though (PRINTLEVEL CARN CDRN) were in effect)
    (COND
      ((NLISTP OBJECT)
	OBJECT)
      ((ZEROP CARN)
	(QUOTE &))
      [(ZEROP CDRN)
	(COND
	  (TAILP (LIST (QUOTE --)))
	  ((QUOTE &]
      ((CONS (PNCHARS2 (CAR OBJECT)
		       (SUB1 CARN)
		       (IPLUS SUMCAR CARN)
		       NIL)
	     (AND (CDR OBJECT)
		  (PNCHARS2 (CDR OBJECT)
			    CARN
			    (SUB1 CDRN)
			    T])

(PPOBJ
  [LAMBDA (OBJ DOIT LMARGIN CPOS WIDTH TOPLEV SQUEEZE OBJPOS)
                                                            (* DD: " 4-NOV-82 19:48")

          (* this is a special purpose pretty printer, intended to be used in conjunction with PRINTOPT.
	  It uses approximately a similar algorithem to the standard. The indented augmentation is that it prints within a 
	  controlled vertical window, and returns the number of lines printed as a value.)



          (* OBJ is the object to be prettyprinted, DOIT=T to actually print, otherwise just calculate the size.
	  LMARGIN is the initial left margin value. CPOS is the current position on the line WIDTH is the right margin and 
	  TOPLEV is a flag to not print the leading and trailing parentheses SQUEEZE is a flag which it T causes 
	  prettyprinting to prefer narrow rather than long lines)


    [COND
      ((NULL LMARGIN)
	(SETQ LMARGIN 0)
	(AND DOIT (NULL TOPLEV)
	     (SPACES LMARGIN]
    (OR WIDTH (SETQ WIDTH (IDIFFERENCE (LINELENGTH)
				       4)))
    (first (COND
	     ((NULL TOPLEV)
	       [COND
		 (DOIT (PRIN3 (QUOTE %(]
	       (add POS 1)))
       for (OBJNUMBER (LINESPRINTED _ 0)
		      (J _ OBJ)
		      NEWLINESPRINTED NEWLINE ITERATE NOTFIRST NEWWIDTH CRPOS
		      (POS _(OR CPOS LMARGIN))
		      I)
       declare (LOCALVARS . T) from 1 while (LISTP J)
       do (SETQ I (CAR J))
	  (SETQ NEWWIDTH (PNCHARS I WIDTH WIDTH NIL NIL (IDIFFERENCE WIDTH POS)))
	  (AND (IGREATERP (IPLUS POS NEWWIDTH 1)
			  WIDTH)
	       [COND
		 ((LISTP ITERATE))
		 ((AND CRPOS (NLISTP I)
		       (ILESSP (IPLUS CRPOS NEWWIDTH)
			       WIDTH]
	       (SETQ NEWLINE T))
	  (COND
	    (NEWLINE [OR CRPOS (SETQ CRPOS (COND
			     (TOPLEV LMARGIN)
			     (T (IPLUS LMARGIN 1]
		     [COND
		       (DOIT (TERPRI)
			     (COND
			       [(AND OBJPOS (ILESSP (NCHARS (SETQ NEWLINE
							      (CONCAT "-" (COND
									((EQ OBJPOS T)
									  OBJNUMBER)
									(T (CONCAT OBJPOS OBJNUMBER)))
								      "-")))
						    CRPOS))
				 (PRIN3 NEWLINE)
				 (SPACES (IDIFFERENCE CRPOS (NCHARS NEWLINE]
			       (T (SPACES CRPOS]
		     (SETQ POS CRPOS)
		     (SETQ NEWLINE)
		     (add LINESPRINTED 1))
	    (NOTFIRST (AND DOIT (PRIN3 " "))
		      (add POS 1)))
	  [AND (NLISTP ITERATE)
	       (COND
		 (ITERATE (SETQ ITERATE OBJ))
		 (T (SETQ ITERATE I]
	  (SETQ NOTFIRST T)
	  [COND
	    [(LISTP I)
	      [SETQ NEWLINESPRINTED (PPOBJ I DOIT (OR CRPOS (IPLUS LMARGIN 1))
					   POS WIDTH NIL SQUEEZE (COND
					     ((EQ OBJPOS T)
					       (CONCAT OBJNUMBER " "))
					     (OBJPOS (CONCAT OBJPOS OBJNUMBER " "]
	      (COND
		((IGREATERP NEWLINESPRINTED 0)
		  (add LINESPRINTED NEWLINESPRINTED)
		  (SETQ NEWLINE T))
		(T (add POS (IMINUS NEWLINESPRINTED))
		   (COND
		     (SQUEEZE (SETQ NEWLINE T]
	    (T (AND DOIT (PRIN4 I))
	       (add POS NEWWIDTH)
	       (OR CRPOS (SETQ CRPOS (COND
		       ((ILESSP (IPLUS POS POS)
				WIDTH)
			 (ADD1 POS))
		       (T (IPLUS 2 LMARGIN]
	  (SETQ J (CDR J))
       finally [COND
		 ((OR J (NULL NOTFIRST))
		   (COND
		     (NOTFIRST (AND DOIT (PRIN3 " . "))
			       (add POS 3)))
		   (AND DOIT (PRIN4 J))
		   (add POS (NCHARS J T]
	       (COND
		 ((NULL TOPLEV)
		   (AND DOIT (PRIN3 (QUOTE %))))
		   (add POS 1)))
	       (RETURN (COND
			 ((ZEROP LINESPRINTED)
			   (IPLUS (IMINUS POS)
				  (OR CPOS 0)))
			 (T LINESPRINTED])

(PRINTCAUTIOUSLY
  [LAMBDA (X LEFT)                                          (* DD: "30-OCT-82 13:06")
                                                            (* this function is an alternate prettyprinter, intended
							    to be used by SHOWPRINT. Some modifications to SHOWPRINT
							    allow a nonstandard prettyprinter to be called)
    (TAB 0 LEFT)
    (PPOBJ (GETOPTIMUM X 500 1000)
	   T
	   (POSITION)
	   NIL NIL (NLISTP X)
	   T])

(PRINTOPTIMUM
  [LAMBDA (OBJECT SPACE FILE USEPRIN2)                      (* DD: "29-OCT-82 20:18")
    (OR SPACE (SETQ SPACE (LINELENGTH)))
    [RESETFORM (PRINTLEVEL (SELPRINTLEVEL OBJECT (IQUOTIENT (ITIMES SPACE 3)
							    4)
					  SPACE USEPRIN2))
	       (COND
		 (USEPRIN2 (PRIN2 OBJECT FILE))
		 (T (PRIN1 OBJECT FILE]
    NIL])

(SELPRINTLEVEL
  [LAMBDA (OBJECT MINLEN MAXLEN USEPRIN2)                   (* DD: " 3-FEB-83 10:21")

          (* select a prinlevel AND prindepth that will print OBJECT in between MINLEN AND MAXLEN characters.
	  Optionally use PRIN2 as the basis of calculations rather than PRIN1.)



          (* this is a troublesome procedure, so tread cautiously if you try to modify it. The goal is to optimize two 
	  interacting parameters as quickly as possible. The shape of the "space" of print length for particular settings of 
	  PRINTLEVEL is hilly -
	  there are no simple hill-climbing methods that will work. The algorithem used here is as follows.
	  Initial values for CARN and CDRN are selected at their extreme reasonable values of 2 for CARN and a function of 
	  MAXLEN for CDRN. Thereafter, alternately CARN and CDRN are binary-searched for a better value in the neighborhood of
	  their current value. We take advantage of the fact that the NCHARS function is monotonicly increasing if only CARN 
	  or CDRN is increased to abort the binary searches early. We also switch to the alternate parameter each time an 
	  improvement is found. We continue the process until either no progress is made or we reach a value in the desired 
	  range.)


    (bind CARN_2
	  (CDRN _(IQUOTIENT MAXLEN 4))
	  OLDBEST_-1
	  BEST_0
	  VAL RANGE while [AND (NEQ OLDBEST BEST)
			       (NOT (AND (IGREATERP MAXLEN BEST)
					 (ILESSP MINLEN BEST]
       first (SETQ VAL (PNCHARS OBJECT CARN CDRN NIL USEPRIN2 MAXLEN)) 
                                                            (* initialize the size attributed to current CARN CDRN)
       do (SETQ OLDBEST BEST)                               (* select a new CDRN by a binary search of the current 
							    CARN, centered around the current value for CDRN)
	  (SETQ RANGE (IQUOTIENT MAXLEN (ITIMES CARN 3)))
	  (bind (LB _(MAX CARN (IDIFFERENCE CDRN RANGE)))
		(UB _(IPLUS CDRN (IMAX RANGE 8)))
		MB_CDRN
		PREV while [AND (NEQ LB UB)
				(NOT (AND (IGREATERP MAXLEN BEST)
					  (ILESSP MINLEN BEST]
	     do [COND
		  ((IGREATERP VAL BEST)
		    (COND
		      ((IGREATERP MAXLEN VAL)
			(SETQ CDRN MB)
			(SETQ LB MB)
			(SETQ BEST VAL)
			(RETURN)))
		    (SETQ UB MB)
		    (SETQ PREV))
		  (T (COND
		       (PREV (RETURN))
		       (T (SETQ LB (ADD1 MB))
			  (SETQ PREV T]
		(SETQ MB (IQUOTIENT (IPLUS UB LB)
				    2))
		(SETQ VAL (PNCHARS OBJECT CARN MB NIL USEPRIN2 MAXLEN)))
                                                            (* select a new CARN by binary search using the current 
							    CDRN centered around the current CARN)
	  (SETQ RANGE (IQUOTIENT MAXLEN (ITIMES CDRN 3)))
	  (bind (LB _(MAX 2 (IDIFFERENCE CARN RANGE)))
		(UB _(IPLUS CARN (IMAX RANGE 4)))
		MB_CARN
		PREV while [AND (NEQ LB UB)
				(NOT (AND (IGREATERP MAXLEN BEST)
					  (ILESSP MINLEN BEST]
	     do [COND
		  ((IGREATERP VAL BEST)
		    (COND
		      ((IGREATERP MAXLEN VAL)
			(SETQ CARN MB)
			(SETQ LB MB)
			(SETQ BEST VAL)
			(RETURN)))
		    (SETQ UB MB)
		    (SETQ PREV))
		  (T (COND
		       (PREV (RETURN))
		       (T (SETQ LB (ADD1 MB))
			  (SETQ PREV T]
		(SETQ MB (IQUOTIENT (IPLUS UB LB)
				    2))
		(SETQ VAL (PNCHARS OBJECT MB CDRN NIL USEPRIN2 MAXLEN)))
       finally (RETURN (CONS CARN CDRN])

(SHOWPRIN2
  [LAMBDA (X FILE RDTBL)                                    (* DD: "13-Dec-82 23:09")
    (COND
      (SYSPRETTYFLG (RESETFORM (OUTPUT FILE)
			       (APPLY* (COND
					 ((GETD SYSPRETTYFLG)
					   SYSPRETTYFLG)
					 (T (QUOTE PRINTDEF)))
				       X T)))
      (T (PRIN2 X FILE RDTBL)))
    X])

(SHOWPRINT
  [LAMBDA (X FILE RDTBL)                                    (* DD: " 9-Dec-82 15:43")
    (COND
      [SYSPRETTYFLG (RESETFORM (OUTPUT FILE)
			       (PROGN (APPLY* (COND
						((GETD SYSPRETTYFLG)
						  SYSPRETTYFLG)
						(T (QUOTE PRINTDEF)))
					      X T)
				      (TERPRI FILE]
      (T (PRINT X FILE RDTBL)))
    X])
)

(RPAQQ PRINTOPTEDITHIST (PRINTOPT.LISP))
(DECLARE: DONTCOPY 

(ADDTOVAR EDITHISTALIST (PRINTOPT.LISP ("26-Dec-80 16:42:17" DD: <DDYER>PRINTOPT.LISP.1
							     (PRINTOPTCOMS PRINTOPTFNS PNCHARS 
									   PNCHARS1 PNCHARS2 
									   PRINTOPTIMUM GETOPTIMUM 
									   SELPRINTLEVEL 
									   PRINTOPTBLOCKS 
									   PRINTOPTEDITHIST)
							     (moved here from my-lisp-macros))
				       (("16-FEB-82 12:24:30" . "26-Dec-80 17:01:05")
					DD:
					(/lisp/ddyer/lisp/init/PRINTOPT.;3 . <DDYER>PRINTOPT.LISP.2)
					(SELPRINTLEVEL PRINTOPTBLOCKS)
					(improved SELPRINTLEVEL to do a binary search rather than a 
						  linear search for bounds))
				       ((" 7-MAY-82 03:26:43" . "16-FEB-82 13:27:24")
					DD:
					(/lisp/ddyer/lisp/init/PRINTOPT.;2 . 
					  /lisp/ddyer/lisp/init/PRINTOPT.;4)
					(GETOPTIMUM PNCHARS PNCHARS1 PNCHARS2 PRINTOPTIMUM PPOBJ 
						    SELPRINTLEVEL)
					(implemented "limit" argument to printdc, which had somehow 
						     escaped. I'm sure i did it before...))
				       (" 9-MAY-82 10:29:57" DD: /lisp/ddyer/lisp/init/PRINTOPT.;3
							     (PPOBJ))
				       ("11-JUN-82 21:41:41" DD: /usr/ddyer/lisp/init/PRINTOPT.;3
							     (PPOBJ))
				       ("11-JUN-82 21:46:02" DD: /usr/ddyer/lisp/init/PRINTOPT.;4
							     (PPOBJ))
				       ("11-JUN-82 21:51:22" DD: /usr/ddyer/lisp/init/PRINTOPT.;5
							     (PPOBJ))
				       ("14-JUN-82 22:39:14" DD: /lisp/ddyer/lisp/init/PRINTOPT.;2
							     (PPOBJ))
				       ("15-JUN-82 00:07:55" DD: /lisp/ddyer/lisp/init/PRINTOPT.;3
							     (PPOBJ))
				       ("29-OCT-82 11:55:54" DD: /lisp/ddyer/lisp/init/PRINTOPT.;2
							     ((FNS SELPRINTLEVEL GETOPTIMUM 
								   PRINTOPTIMUM)))
				       ("29-OCT-82 20:20:09" DD: /lisp/ddyer/lisp/init/PRINTOPT.;3
							     ((FNS SELPRINTLEVEL PRINTOPTIMUM)))
				       ("30-OCT-82 12:19:37" DD: /lisp/ddyer/lisp/init/PRINTOPT.;4
							     ((FNS SELPRINTLEVEL GETOPTIMUM)))
				       ("30-OCT-82 13:08:06" DD: /lisp/ddyer/lisp/init/PRINTOPT.;5
							     ((FNS PRINTCAUTIOUSLY)))
				       ("31-OCT-82 16:16:59" DD: /lisp/ddyer/lisp/init/PRINTOPT.;2
							     ((FNS PPOBJ)))
				       (" 1-NOV-82 19:04:50" DD: /lisp/ddyer/lisp/init/PRINTOPT.;3
							     ((FNS PPOBJ)))
				       (" 4-NOV-82 19:52:57" DD: /lisp/ddyer/lisp/init/PRINTOPT.;5
							     (PPOBJ))
				       (" 9-Dec-82 16:00:00" DD: <DDYER>PRINTOPT..21 (SHOWPRINT))
				       ("13-Dec-82 23:09:35" DD: <DDYER>PRINTOPT..22 (SHOWPRIN2))
				       ("19-Jan-83 14:23:01" DD: <DDYER>PRINTOPT..27 (PPOBJ 
										    SELPRINTLEVEL))
				       (" 3-FEB-83 10:22:18" DD: /lisp/ddyer/lisp/init.4/PRINTOPT.;2
							     (GETOPTIMUM PNCHARS PNCHARS1 PNCHARS2 
									 PRINTOPTIMUM SELPRINTLEVEL)))
)
)

(RPAQQ PRINTOPTBLOCKS ((PRINTOPTBLOCK PRINTOPTIMUM SELPRINTLEVEL GETOPTIMUM PNCHARS PNCHARS1 PNCHARS2
				      (ENTRIES PRINTOPTIMUM GETOPTIMUM SELPRINTLEVEL PNCHARS PNCHARS1 
					       PNCHARS2)
				      (SPECVARS USEPRIN2 SUMCAR LIMIT))))
[DECLARE: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY
(BLOCK: PRINTOPTBLOCK PRINTOPTIMUM SELPRINTLEVEL GETOPTIMUM PNCHARS PNCHARS1 PNCHARS2
	(ENTRIES PRINTOPTIMUM GETOPTIMUM SELPRINTLEVEL PNCHARS PNCHARS1 PNCHARS2)
	(SPECVARS USEPRIN2 SUMCAR LIMIT))
]

(PUTPROPS GETOPTIMUM HELP (returns a form representing OBJECT that will PRIN1 in SPACE or fewer 
				   characters. See also PRINTOPTIMUM))

(PUTPROPS PRINTOPTIMUM HELP (uses SELPRINTLEVEL to select a printlevel that will print OBJECT in 
				  SPACE or fewer characters, and prints it. See also GETOPTIMUM))
(RELINK (QUOTE LISPX))
(DECLARE: DONTCOPY
  (FILEMAP (NIL (517 11408 (GETOPTIMUM 529 . 859) (PNCHARS 863 . 1347) (PNCHARS1 1351 . 2107) (PNCHARS2 
2111 . 2767) (PPOBJ 2771 . 6366) (PRINTCAUTIOUSLY 6370 . 6858) (PRINTOPTIMUM 6862 . 7222) (
SELPRINTLEVEL 7226 . 10711) (SHOWPRIN2 10715 . 11043) (SHOWPRINT 11047 . 11405)))))
STOP
