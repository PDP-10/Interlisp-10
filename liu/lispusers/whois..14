(FILECREATED " 3-JUL-78 03:01:50" <LISPUSERS>WHOIS.;14 3311   


     changes to:  WHOIS

     previous date: " 3-APR-78 01:27:45" <LISPUSERS>WHOIS.;13)


(PRETTYCOMPRINT WHOISCOMS)

(RPAQQ WHOISCOMS ((FNS WHOIS SETUPLOW)
	(BLOCKS (WHOIS WHOIS (NOLINKFNS . T)
		       (GLOBALVARS UPLOW WHOISFILES)))
	(VARS (WHOISFILES (UNION (LISTP (GETATOMVAL (QUOTE WHOISFILES)))
				 (QUOTE (<SECRETARY>PARCPHONEADDRESS.TTY
					  )))))))
(DEFINEQ

(WHOIS
  (LAMBDA (STR FILES VAL)               (* lmm " 3-JUL-78 03:00")
    (SETUPLOW)
    (RESETLST
      (for FILE inside (OR FILES WHOISFILES)
	 do (PROG (NAMEPOS STARTPOS ENDPOS ENTRYBEGIN TEM
			   (FL (INPUT (INFILE FILE)))
			   (FILEBEGIN 0))
	          (RESETSAVE NIL (LIST (FUNCTION CLOSEF?)
				       FL))
	          (SETFILEPTR FL 0)
	      SEARCH
	          (SETQ FILEBEGIN (GETFILEPTR FL))
	          (COND
		    ((NOT (SETQ NAMEPOS
			    (FFILEPOS STR FL NIL NIL NIL NIL UPLOW)))
		      (RETURN)))
	          (SETQ STARTPOS NAMEPOS)
	      BKUP(SETQ ENDPOS STARTPOS)
	          (SETQ STARTPOS (IDIFFERENCE STARTPOS 500))
	          (COND
		    ((ILESSP STARTPOS FILEBEGIN)
		      (SETQ STARTPOS FILEBEGIN)))
	          (SETQ TEM STARTPOS)
	          (SETQ ENTRYBEGIN NIL)
	          (while (SETQ TEM
			   (FILEPOS "
" FL TEM ENDPOS NIL T))
		     do (COND
			  ((NOT (FMEMB (PEEKC FL)
				       (QUOTE (%  %	 %
))))
			    (SETQ ENTRYBEGIN TEM))))
	          (SETFILEPTR FL
			      (OR ENTRYBEGIN
				  (COND
				    ((NOT (IGREATERP STARTPOS FILEBEGIN)
					  )
				      FILEBEGIN)
				    (T (GO BKUP)))))
	          (OR (ZEROP (POSITION T))
		      (TERPRI T))
	          (AND VAL (PRIN1 "------------
" T))
	          (COND
		    ((NEQ VAL FL)
		      (PRIN1 "{from " T)
		      (PRIN2 FL T T)
		      (PRIN1 "}
" T)
		      (SETQ VAL FL)))
	          (RESETVARS ((ERRORTYPELST
				(QUOTE ((16 (RETFROM ERRORPOS NIL T)))))
			      )
			     (do (SELECTQ (SETQ TEM (READC FL))
					  ((NIL % %)
                                        (* control-Z -
					return)
					    (RETURN))
					  (%
                                        (* cr)
 (SELECTQ (PEEKC FL)
	  ((%  %	 %
)                                       (* continue)
	    (PRIN1 TEM T))
	  (RETURN)))
					  (PRIN1 TEM T))))
	          (TERPRI T)
	          (GO SEARCH))))
    (AND VAL T)))

(SETUPLOW
  (LAMBDA NIL
    (COND
      ((NOT (ARRAYP (GETATOMVAL (QUOTE UPLOW))))
	(PROG ((ARR (ARRAY 128 128)))
	      (for I from 0 to 127
		 do
		  (SETA
		    ARR
		    (ADD1 I)
		    (COND
		      ((AND (NOT (ILESSP I (CONSTANT
					   (CHCON1 (QUOTE A)))))
			    (NOT (IGREATERP I
					    (CONSTANT
					      (CHCON1 (QUOTE Z))))))
			(IPLUS I (CONSTANT (IDIFFERENCE
					     (CHCON1 (QUOTE a))
					     (CHCON1 (QUOTE A))))))
		      ((FMEMB I
			      (QUOTE (0 9 10 13 31 32 33 44 45 46 59 63)
				     ))
			0)
		      (T I))))
	      (SETQ UPLOW ARR))))))
)
[DECLARE: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY
(BLOCK: WHOIS WHOIS (NOLINKFNS . T)
	(GLOBALVARS UPLOW WHOISFILES))
]

(RPAQ WHOISFILES (UNION (LISTP (GETATOMVAL (QUOTE WHOISFILES)))
			(QUOTE (<SECRETARY>PARCPHONEADDRESS.TTY))))
(DECLARE: DONTCOPY
  (FILEMAP (NIL (451 3046 (WHOIS 463 . 2439) (SETUPLOW 2443 . 3043)))))
STOP
